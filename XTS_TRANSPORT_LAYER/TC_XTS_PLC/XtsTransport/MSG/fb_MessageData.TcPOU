<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.13">
  <POU Name="fb_MessageData" Id="{2f005f39-acbb-4c78-975a-e5ac037761a2}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ABSTRACT fb_MessageData IMPLEMENTS I_MessageData
VAR CONSTANT
{attribute 'hide'}
  STR_LEN       : UDINT := 524_288;
END_VAR
VAR
  _eCheck       : E_MESSAGE_CHECK;            // pointer check state

  _pFileRoot    : POINTER TO STRING(255);     // you decide (example in GVL_MSG.LOG_FILE)
  _pDirRoot     : POINTER TO STRING(255);     // see GVL_MSG.LOG_DIR

  _nMsgIndex    : REFERENCE TO UINT;          // Hypnos IS the only one in the village, if there are more you need more villages
  _nErrIndex    : REFERENCE TO UINT;          // Thanatos, Hypnos stern and more serious brother

  _dtLocalTime  : POINTER TO DT;
  _pMsgList     : POINTER TO ARRAY[1..GVL_MSG.MAX_MESSAGE] OF ST_Message; // attach your list here

	_rtrigReset   : Tc2_Standard.R_TRIG;
	_aWriteList   : ARRAY[1..GVL_MSG.MAX_MESSAGE] OF ST_Message;
	_sWriteData   : STRING(STR_LEN);
	_sNewLine     : STRING := '$N';
	_sTab         : STRING := '$T';
	_iCount, 
	_iStart, 
	_iEnd         : UINT;

	_fbCreateDir  : Tc2_System.FB_CreateDir;
	_sTmp         : STRING(256);
	_sFilename    : STRING(256);
	_sFolderDate  : STRING;
	_bFileCreated : BOOL;

	_fbFileOpen   : Tc2_System.FB_FileOpen;
	_fbFileWrite  : Tc2_System.FB_FileWrite;
	_fbFileClose  : Tc2_System.FB_FileClose;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="methods" Id="{6efdb5c1-11a9-4aab-a320-a18fab3d716b}" />
    <Folder Name="properties" Id="{3aa3bf69-352d-4c22-bfa5-b104d235f0f7}" />
    <Method Name="Builder" Id="{609cf08e-28ff-42af-b65a-1ba0f90064a0}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD Builder : BOOL // string concat for length > 255
VAR_INPUT
  pInString   : POINTER TO STRING;
  pOutString  : POINTER TO STRING(STR_LEN);
  Length      : UINT;
END_VAR
VAR
  Counter     : UDINT;
  pInWork     : POINTER TO BYTE;
  pOutWork    : POINTER TO BYTE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pOutWork := pOutString;
pInWork := pInString;
Counter := 0;

(*check termination*)
WHILE (pOutWork^ <> 0) AND
      (Counter < STR_LEN+1)
DO
  Counter := Counter + 1;
  pOutWork := pOutWork + 1;
END_WHILE

(* append only if data fits into output *)
IF (Counter + Length < STR_LEN+1)
THEN

  (* append data *)
  FOR Counter := 1 TO Length
  DO
    pOutWork^ := pInWork^;
    pOutWork := pOutWork + 1;
    pInWork := pInWork + 1;
  END_FOR

  (* terminate *)
  pOutWork := pOutWork + 1;
  pOutWork^ := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Check" Id="{6725e7b2-b531-4b00-b57e-f307528efe35}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD Check : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT (_dtLocalTime <> 0)
THEN
  _eCheck       := E_MESSAGE_CHECK.MSG_CHECK_INVALID_POINTER_TIME;
  RETURN;
END_IF

IF NOT (_pMsgList <> 0)
THEN
  _eCheck       := E_MESSAGE_CHECK.MSG_CHECK_INVALID_POINTER_LIST;
  RETURN;
END_IF

IF NOT (_pFileRoot <> 0)
THEN
  _eCheck       := E_MESSAGE_CHECK.MSG_CHECK_INVALID_POINTER_ROOT_FILE;
  RETURN;
END_IF

IF NOT (_pDirRoot <> 0)
THEN
  _eCheck       := E_MESSAGE_CHECK.MSG_CHECK_INVALID_POINTER_ROOT_DIR;
  RETURN;
END_IF

IF NOT __ISVALIDREF(_nMsgIndex)
THEN
  _eCheck       := E_MESSAGE_CHECK.MSG_CHECK_INVALID_POINTER_MSG_IDX;
  RETURN;
END_IF

IF NOT __ISVALIDREF(_nErrIndex)
THEN
  _eCheck       := E_MESSAGE_CHECK.MSG_CHECK_INVALID_POINTER_ERR_IDX;
  RETURN;
END_IF


_eCheck         := E_MESSAGE_CHECK.MSG_CHECK_DONE;
Check           := TRUE;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Device_to_String" Id="{f4058751-b585-4985-bb13-1da3be4530bc}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD Device_to_String : string
VAR_INPUT
  eMsgDevice    : e_Device;
END_VAR
VAR
  sId                 : STRING := '_ID_';
  nId                 : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (eMsgDevice < e_device.General)
THEN
  // below general case
  nId := TO_UDINT(eMsgDevice);
  sId := concat(sId, TO_STRING(nId));

  Device_to_String := concat(TO_STRING(eMsgDevice), sId);

ELSE
  IF (eMsgDevice >= e_Device.DataProduct)
  THEN
    nId := eMsgDevice MOD e_Device.DataProduct;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_Device.DataProduct), sId);

  ELSIF (eMsgDevice >= e_Device.Application)
  THEN
    nId := eMsgDevice MOD e_Device.Application;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_Device.Application), sId);

  ELSIF (eMsgDevice >= e_Device.Process)
  THEN
    nId := eMsgDevice MOD e_Device.Process;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_Device.Process), sId);

  ELSIF (eMsgDevice >= e_Device.Metrics)
  THEN
    nId := eMsgDevice MOD e_Device.Metrics;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Metrics), sId);

  ELSIF (eMsgDevice >= e_device.XfcAxis)
  THEN
    nId := eMsgDevice MOD e_device.XfcAxis;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XfcAxis), sId);

  ELSIF (eMsgDevice >= e_device.Axis)
  THEN
    nId := eMsgDevice MOD e_device.Axis;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Axis), sId);

  ELSIF (eMsgDevice >= e_device.NciChannelCtrl)
  THEN
    nId := eMsgDevice MOD e_device.NciChannelCtrl;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.NciChannelCtrl), sId);

  ELSIF (eMsgDevice >= e_device.NciChannel)
  THEN
    nId := eMsgDevice MOD e_device.NciChannel;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.NciChannel), sId);

  ELSIF (eMsgDevice >= e_Device.Camming)
  THEN
    nId := eMsgDevice MOD e_Device.Camming;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Camming), sId);

  ELSIF (eMsgDevice >= e_Device.XtsStation)
  THEN
    nId := eMsgDevice MOD e_Device.XtsStation;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XtsStation), sId);

  ELSIF (eMsgDevice >= e_Device.XfcTouchProbe)
  THEN
    nId := eMsgDevice MOD e_Device.XfcTouchProbe;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XfcTouchProbe), sId);

  ELSIF (eMsgDevice >= e_Device.XfcTriggerBox)
  THEN
    nId := eMsgDevice MOD e_Device.XfcTriggerBox;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XfcTriggerBox), sId);

  ELSIF (eMsgDevice >= e_Device.XtsTransportUnit)
  THEN
    nId := eMsgDevice MOD e_Device.XtsTransportUnit;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XtsTransportUnit), sId);

  ELSIF (eMsgDevice >= e_Device.MoverCtrl)
  THEN
    nId := eMsgDevice MOD e_Device.MoverCtrl;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.MoverCtrl), sId);

  ELSIF (eMsgDevice >= e_Device.Mover)
  THEN
    nId := eMsgDevice MOD e_Device.Mover;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Mover), sId);

  ELSIF (eMsgDevice >= e_Device.XpuCtrl)
  THEN
    nId := eMsgDevice MOD e_Device.XpuCtrl;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XpuCtrl), sId);

  ELSIF (eMsgDevice >= e_Device.Xpu)
  THEN
    nId := eMsgDevice MOD e_Device.Xpu;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Xpu), sId);

  ELSIF (eMsgDevice >= e_Device.CaGroup)
  THEN
    nId := eMsgDevice MOD e_Device.CaGroup;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.CaGroup), sId);

  ELSIF (eMsgDevice >= e_Device.General)
  THEN
    nId := eMsgDevice MOD e_Device.General;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.General), sId);

  ELSE
    Device_to_String := TO_STRING(eMsgDevice);
  END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Property Name="DirRoot" Id="{e3002335-a9d4-4fa0-babb-4471b2f87f88}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY DirRoot : POINTER to string(255)]]></Declaration>
      <Set Name="Set" Id="{27f9d3c4-cb56-42fc-9b18-6a69116eda5d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_pDirRoot := DirRoot;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="ErrorIndex" Id="{6be1d3dc-65b9-4dfd-907f-082fcdb87e63}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY ErrorIndex : reference to uint]]></Declaration>
      <Set Name="Set" Id="{87b1936c-165f-4106-bf8b-a88ed9b1846f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_nErrIndex  REF= ErrorIndex;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="FileRoot" Id="{82e25f9d-4fd4-444d-84ff-6766e5483898}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY FileRoot : pointer to string(255)]]></Declaration>
      <Set Name="Set" Id="{821478b7-60e9-4231-a97a-89f5913bcee6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_pFileRoot := FileRoot;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="FolderDate" Id="{1db2f735-cb72-461f-b6ed-56e1acb6087a}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD FolderDate : STRING
VAR_INPUT
	LocalTime   : POINTER TO DT;
END_VAR
VAR
	sTmp        : STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT (LocalTime <> 0)
THEN
	FolderDate := 'NULL';
	RETURN;
END_IF

sTmp := DT_TO_STRING(LocalTime^);

// cut off #dt literal
sTmp := right(sTmp, len(sTmp)-3);

sTmp := replace(sTmp, '_', 1, 5);
sTmp := replace(sTmp, '_', 1, 8);

sTmp := left(sTmp,10);	// year_month_day

FolderDate := sTmp;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="LocalTime" Id="{cad2b26a-77b0-49e3-b9a9-abe49b4def11}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY LocalTime : pointer to DT]]></Declaration>
      <Set Name="Set" Id="{3d5c84d2-70cc-42c7-9140-d250d898725a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_dtLocalTime := LocalTime;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="MessageIndex" Id="{e1b0688e-1cac-4b9d-9e06-ea94b3eecaaa}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY MessageIndex : reference to uint]]></Declaration>
      <Set Name="Set" Id="{174624c7-abb8-490d-8f31-8782aeac370a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_nMsgIndex   REF= MessageIndex;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="MsgList" Id="{297e2941-9935-4a61-b087-c9ed90b16846}" FolderPath="properties\">
      <Declaration><![CDATA[PROPERTY MsgList : pointer to ARRAY[1..GVL_MSG.MAX_MESSAGE] OF st_message]]></Declaration>
      <Set Name="Set" Id="{d9725e6e-af36-4a5d-a7da-892bc60e66f4}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_pMsgList := MsgList;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="StringLen" Id="{06c57b65-d27b-44fc-9509-88bd884bd085}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD StringLen : UINT // 	string length for len > 255
VAR_INPUT
  pInString		: POINTER TO STRING(STR_LEN);
END_VAR
VAR
	Counter			: UINT;
	pInWork			: POINTER TO BYTE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pInWork := pInString;
Counter := 0;

WHILE (pInWork^ <> 0) AND
			(Counter < STR_LEN)
DO
	Counter := Counter + 1;
	pInWork := pInWork + 1;
END_WHILE

StringLen := Counter;]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteData" Id="{8c9298e2-ac6d-4844-a0fd-3b84a7d38320}" FolderPath="methods\">
      <Declaration><![CDATA[METHOD WriteData  : e_progress
VAR_INPUT
  bExecute        : BOOL;
END_VAR
VAR_INST
  _eState         : e_progress;
  _bError         : BOOL;
  _nErrorId       : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT bExecute
THEN
  _eState                     := E_PROGRESS.PROGRESS_INIT;
END_IF

CASE _eState 
OF

	E_PROGRESS.PROGRESS_INIT:
		_fbFileOpen.bExecute      := FALSE;
		_fbFileWrite.bExecute	    := FALSE;
		_fbFileClose.bExecute	    := FALSE;

		IF bExecute
		THEN
      _nErrorId               := 0;
      _eState                 := E_PROGRESS.PROGRESS_BUSY;
		END_IF

	E_PROGRESS.PROGRESS_BUSY:
		_fbFileOpen.bExecute      := TRUE;

    IF _fbFileOpen.bError
    THEN
		  _fbFileOpen.bExecute    := FALSE;
      _nErrorId               := _fbFileOpen.nErrId;
      _eState                 := E_PROGRESS.PROGRESS_ERROR;

		ELSIF _fbFileOpen.bBusy
		THEN
      _eState                 := E_PROGRESS.PROGRESS_PREPARE;
		END_IF

	E_PROGRESS.PROGRESS_PREPARE:
		IF NOT _fbFileOpen.bBusy
		THEN
		  _fbFileOpen.bExecute    := FALSE;
			_fbFileWrite.bExecute   := TRUE;
      _eState                 := E_PROGRESS.PROGRESS_STARTUP;
		END_IF

	E_PROGRESS.PROGRESS_STARTUP:
    IF _fbFileWrite.bError
    THEN
			_fbFileWrite.bExecute   := FALSE;
      _nErrorId               := _fbFileWrite.nErrId;
      _eState                 := E_PROGRESS.PROGRESS_ERROR;

		ELSIF _fbFileWrite.bBusy
		THEN
      _eState                 := E_PROGRESS.PROGRESS_CHECK;
		END_IF

	E_PROGRESS.PROGRESS_CHECK:
		IF NOT _fbFileWrite.bBusy
		THEN
			_fbFileWrite.bExecute   := FALSE;
			_fbFileClose.bExecute   := TRUE;
      _eState                 := E_PROGRESS.PROGRESS_OCCUPIED;
		END_IF

	E_PROGRESS.PROGRESS_OCCUPIED:
    IF _fbFileClose.bError
    THEN
			_fbFileClose.bExecute   := FALSE;
      _nErrorId               := _fbFileClose.nErrId;
      _eState                 := E_PROGRESS.PROGRESS_ERROR;

		ELSIF _fbFileClose.bBusy
		THEN
      _eState                 := E_PROGRESS.PROGRESS_WORKING;
		END_IF

	E_PROGRESS.PROGRESS_WORKING:
		IF NOT _fbFileClose.bBusy
		THEN
			_fbFileClose.bExecute   := FALSE;
      _eState                 := E_PROGRESS.PROGRESS_DONE;
		END_IF
END_CASE


_fbFileOpen(
	sNetId:= , 
	sPathName:= _sFilename, 
	nMode:= FOPEN_MODEAPPEND OR FOPEN_MODEPLUS, 
	ePath:= PATH_GENERIC, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> , 
	hFile=> );

_fbFileWrite(
	sNetId:= , 
	hFile:= _fbFileOpen.hFile, 
	pWriteBuff:= ADR(_sWriteData), 
	cbWriteLen:= StringLen(ADR(_sWriteData)), 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> , 
	cbWrite=> );

_fbFileClose(
	sNetId:= , 
	hFile:= _fbFileOpen.hFile, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> );

WriteData                     := _eState;
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="fb_MessageData">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="fb_MessageData.Builder">
      <LineId Id="1" Count="28" />
    </LineIds>
    <LineIds Name="fb_MessageData.Check">
      <LineId Id="1" Count="41" />
    </LineIds>
    <LineIds Name="fb_MessageData.Device_to_String">
      <LineId Id="1" Count="146" />
    </LineIds>
    <LineIds Name="fb_MessageData.DirRoot.Set">
      <LineId Id="1" Count="1" />
    </LineIds>
    <LineIds Name="fb_MessageData.ErrorIndex.Set">
      <LineId Id="1" Count="1" />
    </LineIds>
    <LineIds Name="fb_MessageData.FileRoot.Set">
      <LineId Id="1" Count="1" />
    </LineIds>
    <LineIds Name="fb_MessageData.FolderDate">
      <LineId Id="1" Count="17" />
    </LineIds>
    <LineIds Name="fb_MessageData.LocalTime.Set">
      <LineId Id="1" Count="1" />
    </LineIds>
    <LineIds Name="fb_MessageData.MessageIndex.Set">
      <LineId Id="1" Count="1" />
    </LineIds>
    <LineIds Name="fb_MessageData.MsgList.Set">
      <LineId Id="1" Count="1" />
    </LineIds>
    <LineIds Name="fb_MessageData.StringLen">
      <LineId Id="1" Count="10" />
    </LineIds>
    <LineIds Name="fb_MessageData.WriteData">
      <LineId Id="1" Count="116" />
    </LineIds>
  </POU>
</TcPlcObject>