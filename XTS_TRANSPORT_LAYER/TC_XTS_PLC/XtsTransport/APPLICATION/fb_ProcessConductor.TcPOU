<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.13">
  <POU Name="fb_ProcessConductor" Id="{2e55356c-9eff-4ca2-8ae2-348e0af7a4f1}" SpecialFunc="None">
    <Declaration><![CDATA[//---------------------------------------------------------------------------------------------------------------
//
//  HAUD 2025.12.30
//
//  - fb_ProcessConductor
//  -----------------------------
//  - detects new entry in the cyclic linked list wrapper
//    - forwards the received entry to target instance
//
//  - Ctrl / State for you to use (uses instance ctrl/state pair)
//  - this instance has no transport process
//    - this conductor is used for sending tickets to a transport process list
//---------------------------------------------------------------------------------------------------------------
FUNCTION_BLOCK fb_ProcessConductor EXTENDS fb_Instance
VAR
  _ItfTicketList          : I_Process_LinkedList;
  _ResultTicketList       : ST_PROCESS_LIST_RESULT;

  _WriteResult            : ST_PROCESS_LIST_RESULT;   // last job written to target
  _DeleteResult           : ST_PROCESS_LIST_RESULT;   // last job executed
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// check pointer and interface
// connect Ctrl/State to _ItfProcessCollector
// set message device identification
//-----------------------------------------------------------------------------
IF NOT Check() THEN 
  RETURN; 
END_IF
//-----------------------------------------------------------------------------
// check command
//-----------------------------------------------------------------------------
_eCmd                                 := _stInstanceCtrl[_nProcess].Cmd;
IF (_eCmd <> _eCmdOld)
THEN
  CASE _eCmd
  OF
    E_INSTANCE_CMD.INIT:
      _eStateInstance                 := E_INSTANCE_STATE.NULL;
      _eStateProgress                 := E_PROGRESS.PROGRESS_INIT;
  END_CASE
  CmdLog();
  _eCmdOld                            := _eCmd;
END_IF
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// state machine for ProcessConductor work
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.NULL:
    // first cycle startup
    // initialize linked list, clears all entries
    IF _ItfProcessList[_nProcess].InitList()
    THEN
      _eInit                          := E_PROGRESS.PROGRESS_INVALID;
      _eStateProgress                 := E_PROGRESS.PROGRESS_INIT;
      _eStateInstance                 := E_INSTANCE_STATE.INIT;
    END_IF

  E_INSTANCE_STATE.INIT:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
    // E_INSTANCE_STATE.INIT:
    //-------------------------------------------------------------------------
      E_PROGRESS.PROGRESS_ERROR:
        ;//_eStateProgress               := E_PROGRESS.PROGRESS_INIT;

    // E_INSTANCE_STATE.INIT:
    //-------------------------------------------------------------------------
      E_PROGRESS.PROGRESS_INIT:
        IF (_ItfTicketList <> 0)
        THEN
          _eStateProgress             := E_PROGRESS.PROGRESS_BUSY;
        END_IF
    // E_INSTANCE_STATE.INIT:
    //-------------------------------------------------------------------------
      E_PROGRESS.PROGRESS_BUSY:
        IF _ItfTicketList.InitList()
        THEN
          _eInit                      := E_PROGRESS.PROGRESS_DONE;

          _eStateInstance             := E_INSTANCE_STATE.DISABLED;
          _eStateProgress             := E_PROGRESS.PROGRESS_BUSY;
        ELSE
          _eStateProgress             := E_PROGRESS.PROGRESS_ERROR;
        END_IF
      END_CASE
END_CASE
//-----------------------------------------------------------------------------
// DISABLED:
//  - clear local list result data
//  - wait for enable from the outside
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.DISABLED:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_BUSY:
        CASE _eCmd
        OF
          E_INSTANCE_CMD.ENABLE:      // turn it on!!
            _eStateInstance           := E_INSTANCE_STATE.DETECTION;
            _eStateProgress           := E_PROGRESS.PROGRESS_BUSY;
  
            IF (_MessageLevel > E_MessageType.eMessageInfo)
            THEN
              _stMsg.eType            := E_MessageType.eMessageVerbose;
              _stMsg.eSubdevice       := e_Subdevice.Instance_ProcessConductor;
              _stMsg.iErrorNumber     := 0;
              _stMsg.sText            := concat(TO_STRING(_eStateInstance), ': ');
              _stMsg.sText            := concat(_stMsg.sText, TO_STRING(_eStateProgress));
              f_MessageSet(_stMsg);
            END_IF

        END_CASE
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
// DETECTION: 
//  - check inked list and GetHead()
//  - copy job data
//  - switch on process
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.DETECTION:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_BUSY:
        IF (_eCmd = E_INSTANCE_CMD.DISABLE)
        THEN
          _eStateInstance             := E_INSTANCE_STATE.DISABLED;
          _eStateProgress             := E_PROGRESS.PROGRESS_INIT;

        ELSIF (_ItfTicketList.Count > 0)
        THEN
          // get new job order for my process
          _stListResult               := _ItfTicketList.GetHead();  // the list results carries information about the requirements of the target process (who and where)

          IF (_stListResult.wState <> 0)
          THEN
            // error in list results must not appear
            // if you land here, there is somethng wrong with the initialization or configuration of the linked list
            _eStateProgress           := E_PROGRESS.PROGRESS_ERROR;

            _stMsg.eType              := E_MessageType.eMessageError;
            _stMsg.eSubdevice         := e_Subdevice.Ticket_LinkedList_Com_GetHead;
            _stMsg.iErrorNumber       := _stListResult.wState;
            _stMsg.sText              := concat(TO_STRING(_eStateInstance), ': _ListResult.wState: ');
            _stMsg.sText              := concat(_stMsg.sText, TO_STRING(_eStateProgress));
            f_MessageSet(_stMsg);

          ELSE
            IF (_MessageLevel > E_MessageType.eMessageError)
            THEN
              _stMsg.eType            := E_MessageType.eMessageInfo;
              _stMsg.eSubdevice       := e_Subdevice.Ticket_LinkedList_Com_GetHead;
              _stMsg.iErrorNumber     := 0;
              _stMsg.sText            := concat(TO_STRING(_eStateInstance), ': ');
              _stMsg.sText            := concat(_stMsg.sText, '_ListResult.stData.wTarget: ');
              _stMsg.sText            := concat(_stMsg.sText, _ItfProcessCollector[_nProcess].BitsToString(_stListResult.stData.wTarget));
              f_MessageSet(_stMsg);
            END_IF

            _stProcessData            := _stListResult.stData;  // list result contains data for target process


            IF (_stProcessData.wTarget > 0)
            THEN
              _eStateProgress         := E_PROGRESS.PROGRESS_PREPARE;      // reset substate


              IF (_MessageLevel > E_MessageType.eMessageEmpty)
              THEN
                _stMsg.eType          := E_MessageType.eMessageBatch;
                _stMsg.eSubdevice     := e_Subdevice.Instance_ProcessConductor;
                _stMsg.iErrorNumber   := 0;
                _stMsg.sText          := concat(TO_STRING(_eStateProgress), ': ');
                _stMsg.sText          := concat(_stMsg.sText, TO_STRING(e_message_batch.MSG_BATCH_JOB_STARTED));
                f_MessageSet(_stMsg);
              END_IF
            ELSE
              _ResultTicketList       := _ItfTicketList.RemoveHeadValue();
            END_IF

            // check target mask for entries
            IF NOT (_stProcessData.wTarget > 0)
            THEN
              //_eStateProgress         := E_PROGRESS.PROGRESS_ERROR;

              _stMsg.eType            := E_MessageType.eMessageWarning;
              _stMsg.eSubdevice       := e_Subdevice.Process_LinkedList_CheckData;
              _stMsg.iErrorNumber     := 666;
              _stMsg.sText            := concat(TO_STRING(_eStateInstance), ': ');
              _stMsg.sText            := concat(_stMsg.sText, '_ProcessData.wTarget: ');
              _stMsg.sText            := concat(_stMsg.sText, _ItfProcessCollector[_nProcess].BitsToString(_stProcessData.wTarget));
              f_MessageSet(_stMsg);
            END_IF
          END_IF
        END_IF
    END_CASE
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_PREPARE:
      // check process target plausibility
      IF NOT (_stProcessData.nProcessId > 0) OR
         NOT (_stProcessData.nProcessId < E_INSTANCE.INSTANCE_MAX)
      THEN
        _eStateProgress               := E_PROGRESS.PROGRESS_ERROR;
      ELSE
        _DeleteResult                 := _ItfTicketList.RemoveHeadValue();

        IF (_DeleteResult.wState = 0)
        THEN
          _eStateInstance             := E_INSTANCE_STATE.WORKING;      // go check if my process is switched on
          _eStateProgress             := E_PROGRESS.PROGRESS_BUSY;      // reset substate
        ELSE
          _eStateProgress             := E_PROGRESS.PROGRESS_ERROR;
        END_IF
      END_IF
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
// WORKING
//  - Add ticket in target list
//  - check and log
//  - transition to finish
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.WORKING:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_BUSY:
        // write process ticket, then go back up
        _WriteResult                  := _ItfProcessList[_stProcessData.nProcessId].AddTailValue(_stProcessData);

        IF (_WriteResult.wState <> 0)
        THEN
          // adding to target list must be successful on the first try
          // IF you land here, there is something wrong with initialization or configuration of the linked list
          _eStateProgress             := E_PROGRESS.PROGRESS_ERROR;
        ELSE
          _eStateInstance             := E_INSTANCE_STATE.FINISH;
          _eStateProgress             := E_PROGRESS.PROGRESS_BUSY;

          IF (_MessageLevel > E_MessageType.eMessageInfo)
          THEN
            _stMsg.eType              := E_MessageType.eMessageVerbose;
            _stMsg.eSubdevice         := e_Subdevice.Process_LinkedList_AddTail;
            _stMsg.iErrorNumber       := _stProcessData.nProcessId;
            _stMsg.sText              := concat(TO_STRING(_eStateInstance), ': ');
            _stMsg.sText              := concat(_stMsg.sText, TO_STRING(_eStateProgress));
            f_MessageSet(_stMsg);
          END_IF
        END_IF
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
// FINISH:
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.FINISH:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_BUSY:
        _eStateProgress               := E_PROGRESS.PROGRESS_DONE;
    END_CASE
    // E_INSTANCE_STATE.FINISH:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_DONE:
        CASE _eCmd
        OF
          E_INSTANCE_CMD.FINISH:
            _eStateInstance           := E_INSTANCE_STATE.DISABLED;
            _eStateProgress           := E_PROGRESS.PROGRESS_BUSY;
        END_CASE
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
_stInstanceState[_nProcess].State     := _eStateInstance;
_stInstanceState[_nProcess].Progress  := _eStateProgress;
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
_stInstanceState[_nProcess].StationCount      := _ItfProcessCollector[_nProcess].StationCount;
_stInstanceState[_nProcess].QueueProcessCount := _ItfProcessCollector[_nProcess].QueueProcessCount;
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
]]></ST>
    </Implementation>
    <Property Name="TicketLinkedList" Id="{2988c7cc-1b13-497a-90b1-8cee507b37f2}">
      <Declaration><![CDATA[PROPERTY TicketLinkedList : I_Process_LinkedList]]></Declaration>
      <Set Name="Set" Id="{2ed7dba0-c82b-412c-b54d-08cc643be67f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_ItfTicketList := TicketLinkedList;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="fb_ProcessConductor">
      <LineId Id="1" Count="279" />
    </LineIds>
    <LineIds Name="fb_ProcessConductor.TicketLinkedList.Set">
      <LineId Id="1" Count="1" />
    </LineIds>
  </POU>
</TcPlcObject>