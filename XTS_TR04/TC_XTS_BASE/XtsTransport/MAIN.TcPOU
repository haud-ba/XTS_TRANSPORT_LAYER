<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MAIN" Id="{f2e9720a-bda2-4399-8c7d-d9b6e11179c8}" SpecialFunc="None">
    <Declaration><![CDATA[//---------------------------------------------------------------------------------------------------------------
//
//  HAUD 2024.03.18
//
//  - XTS_TRAINING
//  - example project for setting up transport layer
//  - transport layer members:
//    - CaGroup
//      - access to Group functions
//
//    - Mover
//      - access to MC and CA motion functions
//
//    - Xpu (ProcessingUnit)
//      - access to TcCOM Objects in Environment
//
//    - Transport Control Unit
//      - coordinates members to get XTS in defined states
//
//    - Xts Stations
//      - handshakes with extern control for mover transport
//
//  - global datafields for mrmbers in GVL_XTS namespace
//  - global interface datafields for use in members
//  - structures are assigned by reference for every member
//
//---------------------------------------------------------------------------------------------------------------
// This SOFTWARE is provided as an Exemple by THE PROVIDER "as is" and "with all faults." THE PROVIDER makes no 
// representations or warranties of any kind concerning the safety, suitability, lack of viruses, inaccuracies, 
// typographical errors, or other harmful components of this SOFTWARE. There are inherent dangers in the use of 
// any software, and you are solely responsible for determining whether this SOFTWARE is compatible with your 
// equipment and other software installed on your equipment. You are also solely responsible for the protection 
// of your equipment and backup of your data, and THE PROVIDER will not be liable for any damages you may suffer 
// in connection with using, modifying, or distributing this SOFTWARE.
//---------------------------------------------------------------------------------------------------------------
PROGRAM MAIN
VAR
	bInit 				: BOOL;
  eInit         : E_PROGRESS;

  nStation,
	nMover 				: UINT;

  eTransportState     : E_XTS_TRANSPORT_STATE;
  eTransportResult    : E_PROGRESS;

  eXpuState           : E_XPU_STATE;
  eXpuResult          : E_PROGRESS;
  eXpuCheck           : E_XPU_CHECK;

  sCaGroupState,
  sTransportState,
  sTransportCheck,
  sTransportResult,
  sXpuState,
  sXpuResult,
  sXpuCheck,
  sXpuOpMode,
  sXpuMoverdetectionMode,
  sXpuMoverAssignement    : STRING;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//############################################################################
// 	example for init sequence
//############################################################################	
IF NOT bInit
THEN
  CASE eInit
  OF
    E_PROGRESS.PROGRESS_INVALID,
    E_PROGRESS.PROGRESS_DONE:         // clear command interface
      eInit                           := E_PROGRESS.PROGRESS_BUSY;
      GVL_XTS.XpuCtrl.Cmd             := E_XPU_CTRL.XTS_CMD_NULL;

    E_PROGRESS.PROGRESS_BUSY:         // init Xpu, ProcessingUnit, MotorModules
      eInit                           := E_PROGRESS.PROGRESS_PREPARE;
      GVL_XTS.XpuCtrl.Cmd             := E_XPU_CTRL.XTS_CMD_INIT;

    E_PROGRESS.PROGRESS_PREPARE:      // wait for init to be done
      IF (GVL_XTS.XpuState.State      = E_XPU_STATE.XTS_INIT + E_PROGRESS.PROGRESS_DONE)
      THEN
        eInit                         := E_PROGRESS.PROGRESS_STARTUP;
        GVL_XTS.XpuCtrl.Cmd           := E_XPU_CTRL.XTS_IDLE;
      END_IF

    E_PROGRESS.PROGRESS_STARTUP:      // assign mover interfaces
      eInit                           := E_PROGRESS.PROGRESS_CHECK;
      FOR nMover := 1 TO MAX_MOVER
      DO
        GVL_XTS.MoverItf[nMover]      := GVL_XTS.Mover[nMover];
      END_FOR

    E_PROGRESS.PROGRESS_CHECK:        // assign CaGroup interface
      eInit                           := E_PROGRESS.PROGRESS_OCCUPIED;
      GVL_XTS.CaGroupItf              := GVL_XTS.CaGroup;

    E_PROGRESS.PROGRESS_OCCUPIED:     // StartStation for XtsTransportCtrl.Cmd := E_XTS_TRANSPORT_CTRL.CMD_TRANSPORT_START;
      eInit                           := E_PROGRESS.PROGRESS_WORKING;
      GVL_XTS.StationStart.rPosWait   :=    375.0;
      GVL_XTS.StationStart.rGap       :=    125.0;
      GVL_XTS.StationStart.rVelo      :=   1250.0;
      GVL_XTS.StationStart.rAccDec    :=  12000.0;
      GVL_XTS.StationStart.rJerk      := 400000.0;

    E_PROGRESS.PROGRESS_WORKING:
      bInit                           := TRUE;
      eInit                           := E_PROGRESS.PROGRESS_DONE;
  END_CASE
END_IF

//############################################################################
// 	XTS Transport Unit
//############################################################################
//  - main control function block for transport layer
//  - requires access to member controls
//    - Xpu
//    - CaGroup
//    - Mover
//############################################################################
//
// cyclic call
GVL_XTS.XtsTransport();

// main control
GVL_XTS.XtsTransport.Ctrl             REF= GVL_XTS.XtsTransportCtrl;   // main control
GVL_XTS.XtsTransport.State            REF= GVL_XTS.XtsTransportState;  // main state

// member controls
GVL_XTS.XtsTransport.XpuCtrl          REF= GVL_XTS.XpuCtrl;
GVL_XTS.XtsTransport.XpuState         REF= GVL_XTS.XpuState;
GVL_XTS.XtsTransport.XpuInfo          REF= GVL_XTS.XpuInfo;

GVL_XTS.XtsTransport.GroupItf         :=   GVL_XTS.CaGroupItf;
GVL_XTS.XtsTransport.GroupInfo        REF= GVL_XTS.CaGroupInfo;

GVL_XTS.XtsTransport.MoverItf         REF= GVL_XTS.MoverItf;
GVL_XTS.XtsTransport.MoverInfo        REF= GVL_XTS.MoverInfo;

// start position for E_XTS_TRANSPORT_CTRL.CMD_TRANSPORT_START
GVL_XTS.XtsTransport.StationStart     REF= GVL_XTS.StationStart;
//############################################################################



//############################################################################
// 	XTS Stations
//############################################################################	
FOR nStation := 1 TO MAX_STATION
DO
  GVL_XTS.Station[nStation].StationId   := nStation;

  GVL_XTS.StationListItf[nStation]      := GVL_XTS.StationList[nStation];

  GVL_XTS.Station[nStation].Ctrl        := ADR(GVL_XTS.StationCtrl [nStation]);
  GVL_XTS.Station[nStation].State       := ADR(GVL_XTS.StationState[nStation]);

  GVL_XTS.Station[nStation].ItfStations := ADR(GVL_XTS.StationListItf);
  GVL_XTS.Station[nStation].ItfMover    := ADR(GVL_XTS.MoverItf);
  GVL_XTS.Station[nStation].Mover       := ADR(GVL_XTS.AxisRefMover);
  GVL_XTS.Station[nStation].MoverOffset := ADR(GVL_XTS.PositionOffset);


  GVL_XTS.Station[nStation].StationParameter  := ADR(GVL_XTS.StationParameter);

  // cyclic call
  GVL_XTS.Station[nStation].Cycle(); 
END_FOR
//############################################################################



//############################################################################
// 	XPU environment
//############################################################################	
GVL_XTS.Xpu.XpuInstance               := 1;
GVL_XTS.Xpu.Ctrl                      REF= GVL_XTS.XpuCtrl;
GVL_XTS.Xpu.State                     REF= GVL_XTS.XpuState;
GVL_XTS.Xpu.XpuModuleInfoData         REF= GVL_XTS.XpuModules;

GVL_XTS.Xpu.CaGroupOID                :=   GVL_XTS.CaGroup.GroupInfo.CaGroupOID;

// cyclic call
GVL_XTS.Xpu();
GVL_XTS.XpuInfo                       :=   GVL_XTS.Xpu.XpuInfo;
//############################################################################


//############################################################################	
// 	CA-Group Class
//############################################################################	
// assignment
GVL_XTS.CaGroup.AxisRef               REF= GVL_XTS.AxisRefMover;
GVL_XTS.CaGroup.GroupRef              REF= GVL_XTS.CaGroupRef;

// cyclic call
GVL_XTS.CaGroup.InfoCycle(TRUE); // read group status

// InfoData from group
GVL_XTS.CaGroupInfo                   :=   GVL_XTS.CaGroup.GroupInfo;

// Interface of CaGroup
GVL_XTS.CaGroupItf                    :=   GVL_XTS.CaGroup;
//############################################################################


//############################################################################
// Mover Class	
//############################################################################	
FOR nMover := 1 TO MAX_MOVER
DO
  // assignment	
  GVL_XTS.Mover[nMover].Mover         REF= GVL_XTS.AxisRefMover[nMover];  // axis reference to Mover function block
  GVL_XTS.Mover[nMover].LastGap       REF= GVL_XTS.LastGap[nMover];       // reference to last used gap value datafield
  GVL_XTS.Mover[nMover].LastPosition  REF= GVL_XTS.LastPosition[nMover];  // reference to last position datafield
  
  GVL_XTS.Mover[nMover].RailLength    := GVL_XTS.Xpu.RailLength;          // mover needs RailLength, in order to calc absolute position from modulo input
  
  GVL_XTS.Mover[nMover].MoverInfo		  REF= GVL_XTS.MoverInfo[nMover];     // refernence to mover info data (ActPos, SetPos, ...)

  GVL_XTS.MoverItf[nMover]            := GVL_XTS.Mover[nMover];

  // cyclic call
  GVL_XTS.Mover[nMover].Cycle();  // call AXIS_REF()
END_FOR


// example Mover state machine for ONE selected mover to move manually
// 
MOVER();  // TODO: build FB with Ctrl/State structures 
          //       for extern control to access multiple mover
//############################################################################



//############################################################################
// Visualization data	
//############################################################################	
// get enumerations from XPU for visu:
eTransportState                       := f_GetState(GVL_XTS.XtsTransportState.State);
eTransportResult                      := f_GetProgress(GVL_XTS.XtsTransportState.State);

eXpuState                             := f_GetState(GVL_XTS.XpuState.State);
eXpuResult                            := f_GetProgress(GVL_XTS.XpuState.State);

eXpuCheck                             := GVL_XTS.XpuState.Check;

// display xpu state and result in visu
// get strings and info
sCaGroupState                         := TO_STRING(GVL_XTS.CaGroupInfo.CaGroupState);

sTransportState                       := TO_STRING(eTransportState);
sTransportCheck                       := TO_STRING(GVL_XTS.XtsTransportState.Check);
sTransportResult                      := TO_STRING(eTransportResult);

sXpuState                             := TO_STRING(eXpuState);
sXpuResult                            := TO_STRING(eXpuResult);
sXpuCheck                             := TO_STRING(eXpuCheck);

sXpuOpMode                            := GVL_XTS.Xpu.OpModeToString();
sXpuMoverdetectionMode                := GVL_XTS.Xpu.IdDetectionModeToString();
sXpuMoverAssignement                  := GVL_XTS.Xpu.MoverPositionAssignementToString();

]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="591" Count="2" />
      <LineId Id="432" Count="0" />
      <LineId Id="435" Count="0" />
      <LineId Id="440" Count="0" />
      <LineId Id="442" Count="0" />
      <LineId Id="444" Count="1" />
      <LineId Id="450" Count="0" />
      <LineId Id="447" Count="0" />
      <LineId Id="449" Count="0" />
      <LineId Id="448" Count="0" />
      <LineId Id="454" Count="0" />
      <LineId Id="451" Count="0" />
      <LineId Id="453" Count="0" />
      <LineId Id="452" Count="0" />
      <LineId Id="455" Count="1" />
      <LineId Id="458" Count="0" />
      <LineId Id="457" Count="0" />
      <LineId Id="459" Count="0" />
      <LineId Id="461" Count="0" />
      <LineId Id="460" Count="0" />
      <LineId Id="607" Count="0" />
      <LineId Id="600" Count="0" />
      <LineId Id="602" Count="0" />
      <LineId Id="601" Count="0" />
      <LineId Id="603" Count="0" />
      <LineId Id="608" Count="0" />
      <LineId Id="605" Count="0" />
      <LineId Id="609" Count="0" />
      <LineId Id="606" Count="0" />
      <LineId Id="610" Count="0" />
      <LineId Id="604" Count="0" />
      <LineId Id="755" Count="0" />
      <LineId Id="746" Count="0" />
      <LineId Id="749" Count="1" />
      <LineId Id="752" Count="2" />
      <LineId Id="747" Count="0" />
      <LineId Id="462" Count="1" />
      <LineId Id="443" Count="0" />
      <LineId Id="439" Count="0" />
      <LineId Id="590" Count="0" />
      <LineId Id="554" Count="0" />
      <LineId Id="556" Count="0" />
      <LineId Id="633" Count="0" />
      <LineId Id="613" Count="0" />
      <LineId Id="629" Count="3" />
      <LineId Id="557" Count="0" />
      <LineId Id="619" Count="0" />
      <LineId Id="618" Count="0" />
      <LineId Id="555" Count="0" />
      <LineId Id="627" Count="0" />
      <LineId Id="585" Count="0" />
      <LineId Id="584" Count="0" />
      <LineId Id="586" Count="0" />
      <LineId Id="628" Count="0" />
      <LineId Id="615" Count="2" />
      <LineId Id="655" Count="0" />
      <LineId Id="588" Count="0" />
      <LineId Id="587" Count="0" />
      <LineId Id="589" Count="0" />
      <LineId Id="595" Count="0" />
      <LineId Id="594" Count="0" />
      <LineId Id="598" Count="0" />
      <LineId Id="718" Count="0" />
      <LineId Id="717" Count="0" />
      <LineId Id="719" Count="0" />
      <LineId Id="641" Count="0" />
      <LineId Id="818" Count="5" />
      <LineId Id="828" Count="1" />
      <LineId Id="836" Count="0" />
      <LineId Id="850" Count="0" />
      <LineId Id="848" Count="0" />
      <LineId Id="837" Count="0" />
      <LineId Id="827" Count="0" />
      <LineId Id="832" Count="0" />
      <LineId Id="844" Count="0" />
      <LineId Id="835" Count="0" />
      <LineId Id="838" Count="2" />
      <LineId Id="847" Count="0" />
      <LineId Id="846" Count="0" />
      <LineId Id="843" Count="0" />
      <LineId Id="853" Count="0" />
      <LineId Id="852" Count="0" />
      <LineId Id="851" Count="0" />
      <LineId Id="830" Count="0" />
      <LineId Id="824" Count="2" />
      <LineId Id="614" Count="0" />
      <LineId Id="421" Count="0" />
      <LineId Id="423" Count="1" />
      <LineId Id="428" Count="0" />
      <LineId Id="422" Count="0" />
      <LineId Id="425" Count="0" />
      <LineId Id="611" Count="1" />
      <LineId Id="426" Count="0" />
      <LineId Id="429" Count="0" />
      <LineId Id="427" Count="0" />
      <LineId Id="430" Count="1" />
      <LineId Id="553" Count="0" />
      <LineId Id="640" Count="0" />
      <LineId Id="635" Count="0" />
      <LineId Id="306" Count="1" />
      <LineId Id="389" Count="0" />
      <LineId Id="308" Count="5" />
      <LineId Id="413" Count="0" />
      <LineId Id="314" Count="0" />
      <LineId Id="414" Count="0" />
      <LineId Id="560" Count="0" />
      <LineId Id="559" Count="0" />
      <LineId Id="315" Count="0" />
      <LineId Id="562" Count="0" />
      <LineId Id="639" Count="0" />
      <LineId Id="561" Count="0" />
      <LineId Id="636" Count="0" />
      <LineId Id="316" Count="0" />
      <LineId Id="390" Count="0" />
      <LineId Id="317" Count="9" />
      <LineId Id="564" Count="0" />
      <LineId Id="563" Count="0" />
      <LineId Id="327" Count="2" />
      <LineId Id="332" Count="0" />
      <LineId Id="620" Count="0" />
      <LineId Id="399" Count="0" />
      <LineId Id="401" Count="0" />
      <LineId Id="626" Count="0" />
      <LineId Id="403" Count="0" />
      <LineId Id="756" Count="0" />
      <LineId Id="621" Count="0" />
      <LineId Id="638" Count="0" />
      <LineId Id="637" Count="0" />
      <LineId Id="625" Count="0" />
      <LineId Id="622" Count="2" />
      <LineId Id="480" Count="0" />
      <LineId Id="649" Count="0" />
      <LineId Id="690" Count="0" />
      <LineId Id="648" Count="0" />
      <LineId Id="501" Count="0" />
      <LineId Id="499" Count="0" />
      <LineId Id="531" Count="0" />
      <LineId Id="530" Count="0" />
      <LineId Id="505" Count="0" />
      <LineId Id="472" Count="0" />
      <LineId Id="481" Count="0" />
      <LineId Id="681" Count="0" />
      <LineId Id="680" Count="0" />
      <LineId Id="650" Count="1" />
      <LineId Id="683" Count="0" />
      <LineId Id="654" Count="0" />
      <LineId Id="474" Count="0" />
      <LineId Id="478" Count="1" />
      <LineId Id="507" Count="0" />
      <LineId Id="2" Count="0" />
      <LineId Id="489" Count="1" />
      <LineId Id="644" Count="0" />
      <LineId Id="491" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>