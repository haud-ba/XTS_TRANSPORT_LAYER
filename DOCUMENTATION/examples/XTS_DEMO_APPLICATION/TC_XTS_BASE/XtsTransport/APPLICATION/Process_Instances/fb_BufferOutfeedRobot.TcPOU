<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4026.11">
  <POU Name="fb_BufferOutfeedRobot" Id="{ef2b7354-0dce-40f0-b7c4-f4e8e6320831}" SpecialFunc="None">
    <Declaration><![CDATA[//---------------------------------------------------------------------------------------------------------------
//
//  HAUD 2025.02.20
//
//
//  fb_BufferOutfeedRobot
//  - inherits fb_Instance, which provides a design pattern in order to build different specific processes in a structured way
//  - uses 1 XtsStation via _ItfProcessCollector
//  - used for targeting movers to predetermined substation of process control (one to many)
//  - process checks queue for new movers
//
//
//  - constructed with fb_Instance main state (_eState) in addition to a substate (_eStateProgress)
//  - substate enables to handle procedures within main state by preventing race conditions 
//    with the help of small steps and decisions with clear separation from each other 
//
//---------------------------------------------------------------------------------------------------------------//
// This SOFTWARE is provided as an Exemple by THE PROVIDER "as is" and "with all faults." THE PROVIDER makes no  //
// representations or warranties of any kind concerning the safety, suitability, lack of viruses, inaccuracies,  //
// typographical errors, or other harmful components of this SOFTWARE. There are inherent dangers in the use of  //
// any software, and you are solely responsible for determining whether this SOFTWARE is compatible with your    //
// equipment and other software installed on your equipment. You are also solely responsible for the protection  //
// of your equipment and backup of your data, and THE PROVIDER will not be liable for any damages you may suffer //
// in connection with using, modifying, or distributing this SOFTWARE.                                           //
//---------------------------------------------------------------------------------------------------------------//
FUNCTION_BLOCK fb_BufferOutfeedRobot EXTENDS fb_Instance
VAR
  _nProcessTarget               : ARRAY[1..2] OF E_INSTANCE;     // who am I connected to: OutputR1 and OutputR2

  // target XTS station range
  _nTargetStationLast           : ARRAY[1..2] OF UINT;           // where does the target process end
  _nTargetStationFirst          : ARRAY[1..2] OF UINT;           // where does the target process start

  _rCheckPos                    : LREAL;

  _WriteResult                  : ST_PROCESS_LIST_RESULT;   // last job written to target
  _DeleteResult                 : ST_PROCESS_LIST_RESULT;   // last job executed

  _idx                          : UINT;
  _nTargetIndex                 : ARRAY[1..SIZEOF(T_PROCESS)*8] OF USINT; // helper var to copy to my process (_ItfProcessCollector[_nProcess].TargetIndex)

  _nTargetTotal,                          // how many XtsStations do the target processes have
  _nTargetCounter               : BYTE;   // decremented after every mover, initialized on E_INSTANCE_CMD.INIT

  _MoverInfo                    : ST_MOVER_INFO;      // Information from the Mover in Station
  _StationState                 : ST_STATION_STATE;   // fb_BufferOutfeedRobot has only one XtsStation to work
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// check pointer and interface
// connect Ctrl/State to _ItfProcessCollector
// set message device identification
//-----------------------------------------------------------------------------
IF NOT Check() THEN 
  RETURN; 
END_IF
//-----------------------------------------------------------------------------
// check init command (hard reset)
//-----------------------------------------------------------------------------
_eCmd                                 := _InstanceCtrl[_nProcess].Cmd;
IF (_eCmd <> _eCmdOld)
THEN
  CASE _eCmd
  OF
    E_INSTANCE_CMD.INIT:
      // clear init info
      _eInit                          := _ItfProcessCollector[_nProcess].Init(FALSE);
  
      IF _ItfProcessList[_nProcess].InitList()  // clear linked list
      THEN
        Clear();
        _eStateInstance               := E_INSTANCE_STATE.INIT;
        _eStateProgress               := E_PROGRESS.PROGRESS_INIT;
      END_IF
  END_CASE
  _eCmdOld                            := _eCmd;
END_IF
//-----------------------------------------------------------------------------
// cyclic information of mover and station
//-----------------------------------------------------------------------------
_StationState                         := _ItfProcessCollector[_nProcess].GetStationState  (_ItfProcessCollector[_nProcess].StationLast);
_MoverInfo                            := _ItfProcessCollector[_nProcess].GetMoverInStation(_ItfProcessCollector[_nProcess].StationLast);
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// state machine for buffer to OutputR1 and OutputR2
//-----------------------------------------------------------------------------
CASE _eStateInstance // main state machine
OF
  E_INSTANCE_STATE.NULL:           // first cycle init
    IF _ItfProcessList[_nProcess].InitList()
    THEN
      _eInit                          := _ItfProcessCollector[_nProcess].Init(FALSE);
      _eStateProgress                 := E_PROGRESS.PROGRESS_INIT;
      _eStateInstance                 := E_INSTANCE_STATE.INIT;
    END_IF


  E_INSTANCE_STATE.INIT:              // plausibility checks and _ItfProcessCollector Init
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
    //-------------------------------------------------------------------------
      E_PROGRESS.PROGRESS_ERROR:
        IF (_nProcessTarget[1] > 0) AND
           (_nProcessTarget[1] < E_INSTANCE.INSTANCE_MAX)
        THEN
          IF (_nProcessTarget[2] > 0) AND
             (_nProcessTarget[2] < E_INSTANCE.INSTANCE_MAX)
          THEN
            _eStateProgress           := E_PROGRESS.PROGRESS_INIT;
          END_IF
        END_IF

    // E_INSTANCE_STATE.INIT:
    //-------------------------------------------------------------------------
      E_PROGRESS.PROGRESS_INIT:
        IF NOT (_nProcessTarget[1] > 0) OR
           NOT (_nProcessTarget[1] < E_INSTANCE.INSTANCE_MAX)
        THEN
          // an invalid target you must have not
          _eStateProgress             := E_PROGRESS.PROGRESS_ERROR;
        ELSE
          IF NOT (_nProcessTarget[2] > 0) OR
             NOT (_nProcessTarget[2] < E_INSTANCE.INSTANCE_MAX)
          THEN
            // an invalid target you must have not
            _eStateProgress           := E_PROGRESS.PROGRESS_ERROR;
          ELSE
            _eStateProgress           := E_PROGRESS.PROGRESS_BUSY;
          END_IF
        END_IF

    // E_INSTANCE_STATE.INIT:
    //-------------------------------------------------------------------------
      E_PROGRESS.PROGRESS_BUSY:
        // wait for my process collector check (_State.Check) to be done
        //  - checks range of XtsStations
        //  - checks count of XtsStations
        IF (_State.Check = E_PROCESS_CHECK.PROCESS_CHECK_DONE)
        THEN
          _eInit                      := _ItfProcessCollector[_nProcess].Init(TRUE);
  
          IF (_eInit = E_PROGRESS.PROGRESS_DONE)
          THEN
            IF NOT (_nProcessTarget[1] > 0) OR
               NOT (_nProcessTarget[1] < E_INSTANCE.INSTANCE_MAX)
            THEN
              // an invalid target you must have not
              _eStateProgress         := E_PROGRESS.PROGRESS_ERROR;
            ELSE
              IF NOT (_nProcessTarget[2] > 0) OR
                 NOT (_nProcessTarget[2] < E_INSTANCE.INSTANCE_MAX)
              THEN
                // an invalid target you must have not
                _eStateProgress       := E_PROGRESS.PROGRESS_ERROR;
              ELSE
                FOR _idx := 1 TO 2
                DO 
                  _nTargetStationLast [_idx]  := _ItfProcessCollector[_nProcessTarget[_idx]].StationLast;
                  _nTargetStationFirst[_idx]  := _ItfProcessCollector[_nProcessTarget[_idx]].StationFirst;
                END_FOR
                // get total of xts stations to serve
                _nTargetTotal         := TO_BYTE(_nTargetStationLast [2] - _nTargetStationFirst[1] + 1);
                // init target counter
                _nTargetCounter       := 0;

                // initialization of Infeed: _ItfProcessCollector with XtsStation
                _Ctrl.Cmd             := E_PROCESS_CTRL.PROCESS_DISABLE;  // first, switch off _ItfProcessCollector and XtsStation
                _eStateProgress       := E_PROGRESS.PROGRESS_INIT;        // clear sub state
                _eStateInstance               := E_INSTANCE_STATE.DISABLED;       // switch to disabled as default after startup
              END_IF
            END_IF
          END_IF
        END_IF
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// DISABLED:
//  - clear local list result data
//  - wait for enable from the outside
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.DISABLED:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_INIT:
        _eStateProgress               := E_PROGRESS.PROGRESS_BUSY;
        // clear data for a new round
        memset(ADR(_ListResult), 0, SIZEOF(_ListResult));

        _Ctrl.ActivateStation         := 0;
        _Ctrl.NestMask                := 1;
        
        IF (_nTargetCounter = 0)
        THEN
          // init process bitmask to countdown from
          _ProcessData.wTarget        := _ItfProcessCollector[_nProcess].To_T_Process(_nTargetTotal);
        END_IF

    // E_INSTANCE_STATE.DISABLED:
    //-------------------------------------------------------------------------
      E_PROGRESS.PROGRESS_BUSY:
        CASE _eCmd
        OF
          E_INSTANCE_CMD.ENABLE:      // turn it on!!
            _Ctrl.Cmd                 := E_PROCESS_CTRL.PROCESS_ENABLE;
            _Ctrl.ActivateStation     := _ItfProcessCollector[_nProcess].To_T_Process(TO_BYTE(_ItfProcessCollector[_nProcess].StationCount)); // turn on all stations in my process

            _eStateInstance           := E_INSTANCE_STATE.ENABLED;
            _eStateProgress           := E_PROGRESS.PROGRESS_INIT;
  
            IF (_MessageLevel = E_MessageType.eMessageVerbose)
            THEN
              _stMsg.eType            := E_MessageType.eMessageVerbose;
              _stMsg.eSubdevice       := e_Subdevice.Instance_BufferOutfeedRobot;
              _stMsg.iErrorNumber     := 0;
              _stMsg.sText            := concat(TO_STRING(_eStateInstance), ': ');
              _stMsg.sText            := concat(_stMsg.sText, TO_STRING(_eStateProgress));
              f_MessageSet(_stMsg);
            END_IF

        END_CASE
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
// ENABLED
//  - check process for enabled station
//  - switch to infeed
//  - check if infeed is active
//  - allow movers to enter
//  - check if mover arrived at PosStop[1]
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.ENABLED:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_INIT:
        CASE _State.State
        OF
          E_PROCESS_STATE.PROCESS_ENABLE:
          CASE _State.Result
          OF
            E_PROGRESS.PROGRESS_BUSY:
              // now my process and all XtsStations within are switched on
              // check infeed into my process
              _Ctrl.Cmd               := E_PROCESS_CTRL.PROCESS_STATIONS_INFEED; // tells my process to go check the infeed
              _eStateProgress         := E_PROGRESS.PROGRESS_BUSY; // go check how the infeed into my process is doing

              IF (_MessageLevel = E_MessageType.eMessageVerbose)
              THEN
                _stMsg.eType          := E_MessageType.eMessageVerbose;
                _stMsg.eSubdevice     := e_Subdevice.Instance_BufferOutfeedRobot;
                _stMsg.iErrorNumber   := 0;
                _stMsg.sText          := concat(TO_STRING(_eStateInstance), ': ');
                _stMsg.sText          := concat(_stMsg.sText, TO_STRING(_eStateProgress));
                f_MessageSet(_stMsg);
              END_IF
          END_CASE
        END_CASE
    END_CASE
    // E_INSTANCE_STATE.ENABLED:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_BUSY:
        CASE _State.State
        OF
          E_PROCESS_STATE.PROCESS_STATIONS_INFEED:
          CASE _State.Result
          OF
            E_PROGRESS.PROGRESS_INIT:
              CASE _eCmd
              OF
                E_INSTANCE_CMD.DISABLE:
                  _eStateInstance       := E_INSTANCE_STATE.DISABLED;
                  _eStateProgress       := E_PROGRESS.PROGRESS_INIT;
              END_CASE

            E_PROGRESS.PROGRESS_BUSY:
              // now we know who wants to enter
              // check if mover crossed CheckPosition

              IF (_MoverInfo.rModuloActPos > _rCheckPos)
              THEN
                // now we know who wants to enter
                _Ctrl.Cmd             := E_PROCESS_CTRL.PROCESS_STATIONS_SEND;  // send mover to outfeed process
                _eStateProgress       := E_PROGRESS.PROGRESS_PREPARE;           // go check sending of mover
  
                IF (_MessageLevel = E_MessageType.eMessageVerbose)
                THEN
                  _stMsg.eType        := E_MessageType.eMessageVerbose;
                  _stMsg.eSubdevice   := e_Subdevice.Instance_BufferOutfeedRobot;
                  _stMsg.iErrorNumber := 0;
                  _stMsg.sText        := concat(TO_STRING(_Ctrl.Cmd), ': ');
                  _stMsg.sText        := concat(_stMsg.sText, TO_STRING(_eStateProgress));
                  f_MessageSet(_stMsg);
  
                  _stMsg.eType        := E_MessageType.eMessageVerbose;
                  _stMsg.eSubdevice   := e_Subdevice.Instance_BufferOutfeedRobot;
                  _stMsg.iErrorNumber := 0;
                  _stMsg.sText        := concat(TO_STRING(_eStateInstance), ': ');
                  _stMsg.sText        := concat(_stMsg.sText, TO_STRING(_eStateProgress));
                  f_MessageSet(_stMsg);
                END_IF
              END_IF
          END_CASE
        END_CASE
    END_CASE
    // E_INSTANCE_STATE.ENABLED:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_PREPARE:
        CASE _State.State
        OF
          E_PROCESS_STATE.PROCESS_STATIONS_OUTFEED:
          CASE _State.Result
          OF
            E_PROGRESS.PROGRESS_BUSY:
              // mover is ready to leave
              _eStateInstance         := E_INSTANCE_STATE.FINISH;     // go send mover
              _eStateProgress         := E_PROGRESS.PROGRESS_INIT;    // reset substate

              IF (_MessageLevel = E_MessageType.eMessageVerbose)
              THEN
                _stMsg.eType          := E_MessageType.eMessageVerbose;
                _stMsg.eSubdevice     := e_Subdevice.Instance_BufferOutfeedRobot;
                _stMsg.iErrorNumber   := 0;
                _stMsg.sText          := concat(TO_STRING(_eStateInstance), ': ');
                _stMsg.sText          := concat(_stMsg.sText, TO_STRING(_eStateProgress));
                f_MessageSet(_stMsg);
              END_IF
          END_CASE
        END_CASE
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
// FINISH:
//  - write target (OutputR2 and OutputR1)
//  - command outfeed of mover
//  - AddTail() at TargetProcess
//  - check to proceed
//-----------------------------------------------------------------------------
CASE _eStateInstance
OF
  E_INSTANCE_STATE.FINISH:
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_INIT:
      _eStateProgress                 := E_PROGRESS.PROGRESS_BUSY;

      // the sending of mover
      // one mover at a time
      _nTargetIndex[1]                := TO_USINT(_nTargetStationLast[2]-_nTargetCounter);

      _ItfProcessCollector[_nProcess].TargetIndex := _nTargetIndex; // write Xts target onto my ProcessCollector interface
      _ProcessData.wTarget                        := _ItfProcessCollector[_nProcess].
                                                     DelBit(_ProcessData.wTarget, _nTargetTotal-_nTargetCounter);

      _nTargetCounter                 := _nTargetCounter + 1;

      IF (_nTargetStationLast[2]-_nTargetCounter < _nTargetStationFirst[1])
      THEN
        _nTargetCounter               := 0;
      END_IF

    END_CASE
    //-------------------------------------------------------------------------
    CASE _eStateProgress
    OF
      E_PROGRESS.PROGRESS_BUSY:
        CASE _State.State
        OF
          E_PROCESS_STATE.PROCESS_STATIONS_OUTFEED: // check my process' state
          CASE _State.Result
          OF
            E_PROGRESS.PROGRESS_BUSY:
            // now data is prepared and copied to the right places
            CASE _eCmd
            OF
              E_INSTANCE_CMD.FINISH:      // send mover out!!
                _Ctrl.Cmd             := E_PROCESS_CTRL.PROCESS_STATIONS_OUTFEED; // release the kraken!
                _eStateProgress       := E_PROGRESS.PROGRESS_DONE;                // mover is on its way, we're done here
      
                IF (_MessageLevel = E_MessageType.eMessageVerbose)
                THEN
                  _stMsg.eType        := E_MessageType.eMessageVerbose;
                  _stMsg.eSubdevice   := e_Subdevice.Instance_BufferOutfeedRobot;
                  _stMsg.iErrorNumber := 0;
                  _stMsg.sText        := concat(TO_STRING(_eStateInstance), ': ');
                  _stMsg.sText        := concat(_stMsg.sText, TO_STRING(_eStateProgress));
                  f_MessageSet(_stMsg);
                END_IF
            END_CASE
          END_CASE
        END_CASE

      E_PROGRESS.PROGRESS_DONE:
        CASE _State.State
        OF
          E_PROCESS_STATE.PROCESS_DISABLE: // check my process' state
          CASE _State.Result
          OF
            E_PROGRESS.PROGRESS_BUSY:
              _eStateInstance         := E_INSTANCE_STATE.DISABLED;
              _eStateProgress         := E_PROGRESS.PROGRESS_INIT;
          END_CASE
        END_CASE
    END_CASE
END_CASE
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
_InstanceState[_nProcess].State       := _eStateInstance;
_InstanceState[_nProcess].Progress    := _eStateProgress;
]]></ST>
    </Implementation>
    <Property Name="CheckPosition" Id="{1bc3588a-616a-44c2-9f23-3a89ad2ece55}">
      <Declaration><![CDATA[PROPERTY CheckPosition : lreal]]></Declaration>
      <Get Name="Get" Id="{3bd296a3-599f-47d6-ab64-cf5bdf404121}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[CheckPosition := _rCheckPos;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{0984132b-427f-44bd-8808-a0bcdd26aa99}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_rCheckPos := CheckPosition;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="Clear" Id="{1613c7ab-70bf-422f-b226-32c258827764}">
      <Declaration><![CDATA[METHOD Clear : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[memset(ADR(_nTargetIndex), 0, SIZEOF(_nTargetIndex));
_nTargetTotal                   := 0;
_nTargetCounter                 := 0;

//_eStateInstance                 := E_INSTANCE_STATE.DISABLED;
_eStateProgress                 := E_PROGRESS.PROGRESS_INIT;

Clear                           := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="ProcessTarget" Id="{b582d417-d18c-4ca7-8042-2ade83481269}">
      <Declaration><![CDATA[PROPERTY ProcessTarget : array[1..2] of E_INSTANCE ]]></Declaration>
      <Get Name="Get" Id="{0443b5f5-af97-4410-8097-b513f3264cc5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[ProcessTarget := _nProcessTarget;
]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{c24724a0-8d71-445f-a5b6-979bef20f52c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_nProcessTarget := ProcessTarget;
]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="fb_BufferOutfeedRobot">
      <LineId Id="3621" Count="22" />
      <LineId Id="4067" Count="0" />
      <LineId Id="3644" Count="203" />
      <LineId Id="4069" Count="6" />
      <LineId Id="4068" Count="0" />
      <LineId Id="3848" Count="4" />
      <LineId Id="3854" Count="21" />
      <LineId Id="3897" Count="91" />
      <LineId Id="4049" Count="0" />
      <LineId Id="3993" Count="0" />
      <LineId Id="4051" Count="5" />
      <LineId Id="4062" Count="1" />
      <LineId Id="4065" Count="1" />
      <LineId Id="3996" Count="5" />
      <LineId Id="139" Count="0" />
    </LineIds>
    <LineIds Name="fb_BufferOutfeedRobot.CheckPosition.Get">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="fb_BufferOutfeedRobot.CheckPosition.Set">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="fb_BufferOutfeedRobot.Clear">
      <LineId Id="6" Count="0" />
      <LineId Id="8" Count="1" />
      <LineId Id="13" Count="2" />
      <LineId Id="11" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="fb_BufferOutfeedRobot.ProcessTarget.Get">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="fb_BufferOutfeedRobot.ProcessTarget.Set">
      <LineId Id="2" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>