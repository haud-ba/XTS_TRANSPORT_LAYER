<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MessageData" Id="{de44dfc9-febf-465b-9bb6-e18a439a0b7a}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MessageData
VAR_INPUT
  bActivate     : BOOL;   // activates cyclic copy of message list to visualization; switched on in T_MESSAGE_LIST visu
	bPowerFail		: BOOL;   // UPS switch
	bWriteFile		: BOOL;   // manual write of list
	bReset				: BOOL;
	pList					: POINTER TO ARRAY[1..GVL_MSG.MAX_MESSAGE] OF ST_Message; // attach your list here
	pLocalTime		: POINTER TO DT; 
END_VAR
VAR_OUTPUT
	bBusy					: BOOL;
  bDone         : BOOL;
	VisuList		  : ARRAY[1..GVL_MSG.MAX_MESSAGE] OF ST_Message;
END_VAR
VAR
	eState 				: E_MESSAGE_STATE;

	rtrigReset		: Tc2_Standard.R_TRIG;
	aWriteList		: ARRAY[1..GVL_MSG.MAX_MESSAGE] OF ST_Message;
	sWriteData		: STRING(32767);
	sNewLine			: STRING := '$N';
	sTab					: STRING := '$T';
	iCount, 
	iStart, 
	iEnd					: UINT;

	fbCreateDir		: Tc2_System.FB_CreateDir;
	sTmp          : STRING(256);
	sFilename			: STRING(256);
	sFolderDate		: STRING;
	bFileCreated	: BOOL;

	fbFileOpen		  : Tc2_System.FB_FileOpen;
	fbFileWrite		  : Tc2_System.FB_FileWrite;
	fbFileClose		  : Tc2_System.FB_FileClose;

  ivis          : UINT;

  bBusyWrite,
  bErrorWrite   : BOOL;
  nErrorWrite   : UDINT;
  nStateWrite   : UINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//-----------------------------------------------------------------------------
// check pointers
//-----------------------------------------------------------------------------
IF NOT (pList <> 0) OR
   NOT (pLocalTime <> 0)
THEN
  eState                      := MSG_INVALID_POINTERS;
  RETURN;
END_IF
//-----------------------------------------------------------------------------
// check date
// new message file for a new day
//-----------------------------------------------------------------------------
IF (FolderDate(pLocalTime) <> sFolderDate)
THEN
  bFileCreated                := FALSE;
END_IF
//-----------------------------------------------------------------------------
//----------------------------------------------------------------------------- 
rtrigReset(CLK:= bReset);
IF rtrigReset.Q
THEN
  bReset                      := FALSE;
  eState                      := MSG_NULL;
END_IF
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_NULL:
    bBusy                     := 
    bDone                     := FALSE;
    
    IF (bWriteFile) OR (bPowerFail)
    THEN
      eState                  := MSG_INIT;

      IF (GVL_MSG.MessageIndex = 0)
      THEN
        bBusy                 := TRUE;
        eState                := MSG_WRITE_FILE_DONE;
      END_IF
    END_IF
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_INIT:
    WriteData(FALSE);

    // clear intern list
    memset(ADR(aWriteList),     0, SIZEOF(aWriteList));

    // copy from attached list to local
    memcpy(ADR(aWriteList), pList, SIZEOF(aWriteList));

    // clear attached list
    memset(pList, 0, SIZEOF(aWriteList));

    bBusy                     := TRUE;
    iStart 	                  := 1;
    iEnd 		                  := SEL((GVL_MSG.MessageIndex > GVL_MSG.MAX_MESSAGE-1),
                                      GVL_MSG.MessageIndex,
                                      GVL_MSG.MAX_MESSAGE);
    GVL_MSG.MessageIndex      := 0;

    IF NOT bFileCreated
    THEN
      // folder and filename have to be created
      eState                  := MSG_ROOT;
    ELSE
      // folder and file already exist
      eState                  := MSG_LINE_START;
    END_IF
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_ROOT:
    eState                    := MSG_ROOT_CREATE;
    fbCreateDir.sPathName     := left(GVL_MSG.LOG_DIR,len(GVL_MSG.LOG_DIR)-1);
    fbCreateDir(
            bExecute	        := FALSE);
  
  MSG_ROOT_CREATE:
    (* create root folder *)
    fbCreateDir(
            bExecute	        := TRUE);
    IF fbCreateDir.bBusy
    THEN
      eState                  := MSG_ROOT_CREATE_BUSY;
    END_IF
  
  MSG_ROOT_CREATE_BUSY:
    fbCreateDir(
            bExecute	        := TRUE);
    IF NOT fbCreateDir.bBusy
    THEN
      eState                  := MSG_FOLDER;
    END_IF
  
  MSG_FOLDER:
    eState                    := MSG_FOLDER_CREATE;
    sFolderDate               := FolderDate(pLocalTime);
    fbCreateDir.sPathName     := concat(GVL_MSG.LOG_DIR, sFolderDate);
    fbCreateDir(
            bExecute	        := FALSE);

  MSG_FOLDER_CREATE:
    (* create active folder *)
    fbCreateDir(
            bExecute	        := TRUE);
    IF fbCreateDir.bBusy
    THEN
      eState                  := MSG_FOLDER_CREATE_BUSY;
    END_IF
  
  MSG_FOLDER_CREATE_BUSY:
    fbCreateDir(
            bExecute	        := TRUE);
    IF NOT fbCreateDir.bBusy
    THEN
      eState                  := MSG_FILE_CREATE_DONE;
      fbCreateDir(
              bExecute	      := FALSE);
    END_IF
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_FILE_CREATE_DONE:
    eState                    := MSG_LINE_START;
    bFileCreated              := TRUE;
    // build complete file path
    sTmp                      := concat(concat(GVL_MSG.LOG_FILE, sFolderDate),'.txt');
    sTmp                      := concat('\', sTmp);
    sFilename                 := concat(fbCreateDir.sPathName, sTmp);
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_LINE_START:
    GVL_MSG.MessageError      := 0;
    eState                    := MSG_LINE_BUILD;
    iCount                    := iStart;
    memset(ADR(sWriteData),0,SIZEOF(sWriteData));
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_LINE_BUILD:
    eState                    := MSG_LINE_CHECK;
    IF (aWriteList[iCount].eType > E_MessageType.eMessageEmpty)
    THEN
      sTmp                    := TO_STRING(aWriteList[iCount].eType);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, sTab);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, aWriteList[iCount].tTime);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, sTab);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, Device_To_String(aWriteList[iCount].eDevice));
      sTmp                    := Tc2_Standard.CONCAT(sTmp, sTab);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, TO_STRING(aWriteList[iCount].eSubdevice));
      sTmp                    := Tc2_Standard.CONCAT(sTmp, sTab);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, TO_STRING(aWriteList[iCount].iErrorNumber));
      sTmp                    := Tc2_Standard.CONCAT(sTmp, sTab);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, aWriteList[iCount].sText);
      sTmp                    := Tc2_Standard.CONCAT(sTmp, sNewLine);
      Builder(ADR(sTmp), ADR(sWriteData), StringLen(ADR(sTmp)));
    END_IF
    iCount                    := iCount + 1;
END_CASE
//-----------------------------------------------------------------------------
CASE eState OF
  MSG_LINE_CHECK:
  IF NOT (iCount > iEnd)
  THEN
    eState                    := MSG_LINE_BUILD;
  ELSE
    IF (StringLen(ADR(sWriteData)) > 1)
    THEN
      eState                  := MSG_WRITE_FILE_BUSY;
    ELSE
      eState                  := MSG_WRITE_FILE_DONE;
    END_IF
  END_IF
  
  MSG_WRITE_FILE_BUSY:
  IF WriteData(TRUE)
  THEN
    eState                    := MSG_WRITE_FILE_END;
  END_IF
  
  MSG_WRITE_FILE_END:
  WriteData(FALSE);
  eState                      := MSG_WRITE_FILE_DONE;
  
  MSG_WRITE_FILE_DONE:
  bDone                       := TRUE;
  IF NOT (bWriteFile)
  THEN
    eState                    := MSG_NULL;
  END_IF
END_CASE
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
// visu message list
//-----------------------------------------------------------------------------
memset(ADR(VisuList),0,SIZEOF(VisuList));
// copy last written message list to visu message list
memcpy(ADR(VisuList), ADR(aWriteList), SIZEOF(VisuList));
//-----------------------------------------------------------------------------
]]></ST>
    </Implementation>
    <Method Name="Builder" Id="{f247003e-2aa1-4910-aa2a-0a2ab8a6c9f5}">
      <Declaration><![CDATA[METHOD Builder : BOOL // string concat for length > 255
VAR_INPUT
  pInString   : POINTER TO STRING;
  pOutString  : POINTER TO STRING(32767);
  Length      : UINT;
END_VAR
VAR
  Counter     : DINT;
  pInWork     : POINTER TO BYTE;
  pOutWork    : POINTER TO BYTE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pOutWork := pOutString;
pInWork := pInString;
Counter := 0;

(*check termination*)
WHILE (pOutWork^ <> 0) AND
      (Counter < 32767)
DO
  Counter := Counter + 1;
  pOutWork := pOutWork + 1;
END_WHILE

(* append only if data fits into output *)
IF (Counter + Length < 32767)
THEN

  (* append data *)
  FOR Counter := 1 TO Length
  DO
    pOutWork^ := pInWork^;
    pOutWork := pOutWork + 1;
    pInWork := pInWork + 1;
  END_FOR

  (* terminate *)
  pOutWork := pOutWork + 1;
  pOutWork^ := 0;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Device_to_String" Id="{b14d0ea2-0a55-49cd-a8af-196ccac3719d}">
      <Declaration><![CDATA[METHOD Device_to_String : string
VAR_INPUT
  eMsgDevice    : e_Device;
END_VAR
VAR
  sId                 : STRING := '_ID_';
  nId                 : UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (eMsgDevice <= e_device.General)
THEN
  // general case with no added indices
  Device_to_String := TO_STRING(eMsgDevice);
ELSE
  IF (eMsgDevice >= e_Device.Data)
  THEN
    nId := eMsgDevice MOD e_Device.Data;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Data), sId);

  ELSIF (eMsgDevice >= e_device.XfcAxis)
  THEN
    nId := eMsgDevice MOD e_device.XfcAxis;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XfcAxis), sId);

  ELSIF (eMsgDevice >= e_device.Axis)
  THEN
    nId := eMsgDevice MOD e_device.Axis;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Axis), sId);

  ELSIF (eMsgDevice >= e_device.NciChannelCtrl)
  THEN
    nId := eMsgDevice MOD e_device.NciChannelCtrl;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.NciChannelCtrl), sId);

  ELSIF (eMsgDevice >= e_device.NciChannel)
  THEN
    nId := eMsgDevice MOD e_device.NciChannel;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.NciChannel), sId);

  ELSIF (eMsgDevice >= e_Device.Camming)
  THEN
    nId := eMsgDevice MOD e_Device.Camming;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Camming), sId);

  ELSIF (eMsgDevice >= e_Device.XtsStation)
  THEN
    nId := eMsgDevice MOD e_Device.XtsStation;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XtsStation), sId);

  ELSIF (eMsgDevice >= e_Device.XfcTouchProbe)
  THEN
    nId := eMsgDevice MOD e_Device.XfcTouchProbe;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XfcTouchProbe), sId);

  ELSIF (eMsgDevice >= e_Device.XfcTriggerBox)
  THEN
    nId := eMsgDevice MOD e_Device.XfcTriggerBox;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XfcTriggerBox), sId);

  ELSIF (eMsgDevice >= e_Device.XtsTransportUnit)
  THEN
    nId := eMsgDevice MOD e_Device.XtsTransportUnit;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XtsTransportUnit), sId);

  ELSIF (eMsgDevice >= e_Device.MoverCtrl)
  THEN
    nId := eMsgDevice MOD e_Device.MoverCtrl;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.MoverCtrl), sId);

  ELSIF (eMsgDevice >= e_Device.Mover)
  THEN
    nId := eMsgDevice MOD e_Device.Mover;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Mover), sId);

  ELSIF (eMsgDevice >= e_Device.XpuCtrl)
  THEN
    nId := eMsgDevice MOD e_Device.XpuCtrl;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.XpuCtrl), sId);

  ELSIF (eMsgDevice >= e_Device.Xpu)
  THEN
    nId := eMsgDevice MOD e_Device.Xpu;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.Xpu), sId);

  ELSIF (eMsgDevice >= e_Device.CaGroup)
  THEN
    nId := eMsgDevice MOD e_Device.CaGroup;
    sId := concat(sId, TO_STRING(nId));
 
    Device_to_String := concat(TO_STRING(e_device.CaGroup), sId);

  ELSE
    Device_to_String := TO_STRING(eMsgDevice);
  END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FolderDate" Id="{6323d7b1-8a13-4d31-8062-0c442deb810d}">
      <Declaration><![CDATA[METHOD FolderDate : STRING
VAR_INPUT
	pLocalTime		: POINTER TO DT;
END_VAR
VAR
	sTmp					: STRING;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT (pLocalTime <> 0)
THEN
	FolderDate := 'NULL';
	RETURN;
END_IF

sTmp := DT_TO_STRING(pLocalTime^);

// cut off #dt literal
sTmp := right(sTmp, len(sTmp)-3);

sTmp := replace(sTmp, '_', 1, 5);
sTmp := replace(sTmp, '_', 1, 8);

sTmp := left(sTmp,10);	// year_month_day

FolderDate := sTmp;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="StringLen" Id="{5e86732b-61bf-4490-b37b-6fbfd331e8a8}">
      <Declaration><![CDATA[METHOD StringLen : UINT // 	string length for len > 255
VAR_INPUT
  pInString		: POINTER TO STRING(32767);
END_VAR
VAR
	Counter			: UINT;
	pInWork			: POINTER TO BYTE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pInWork := pInString;
Counter := 0;

WHILE (pInWork^ <> 0) AND
			(Counter < 32767)
DO
	Counter := Counter + 1;
	pInWork := pInWork + 1;
END_WHILE

StringLen := Counter;]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteData" Id="{e9c2aa30-591e-40b9-8778-31f0b8013e79}">
      <Declaration><![CDATA[METHOD WriteData  : BOOL
VAR_INPUT
  bExecute        : BOOL;
END_VAR
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT bExecute
THEN
  bBusyWrite                := FALSE;
	nStateWrite               := 0;
END_IF

CASE nStateWrite OF

	0:
		fbFileOpen.bExecute		  := FALSE;
		fbFileWrite.bExecute	  := FALSE;
		fbFileClose.bExecute	  := FALSE;

		IF bExecute
		THEN
			bBusyWrite            := TRUE;
			nStateWrite           := 10;
		END_IF

	10:
		fbFileOpen.bExecute     := TRUE;
		IF fbFileOpen.bBusy
		THEN
			nStateWrite           := 20;
		END_IF

	20:
		IF NOT fbFileOpen.bBusy
		THEN
			fbFileOpen.bExecute   := FALSE;
			fbFileWrite.bExecute  := TRUE;
			nStateWrite           := 30;
		END_IF

	30:
		IF fbFileWrite.bBusy
		THEN
			nStateWrite           := 40;
		END_IF

	40:
		IF NOT fbFileWrite.bBusy
		THEN
			fbFileWrite.bExecute  := FALSE;
			fbFileClose.bExecute  := TRUE;
			nStateWrite           := 50;
		END_IF

	50:
		IF fbFileClose.bBusy
		THEN
			nStateWrite           := 60;
		END_IF

	60:
		IF NOT fbFileClose.bBusy
		THEN
			fbFileClose.bExecute  := FALSE;
			nStateWrite 		      := 100;
		END_IF
	
	100:
    WriteData               := TRUE;
END_CASE


fbFileOpen(
	sNetId:= , 
	sPathName:= sFileName, 
	nMode:= FOPEN_MODEAPPEND, 
	ePath:= PATH_GENERIC, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> , 
	hFile=> );

fbFileWrite(
	sNetId:= , 
	hFile:= fbFileOpen.hFile, 
	pWriteBuff:= ADR(sWriteData), 
	cbWriteLen:= StringLen(ADR(sWriteData)), 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> nErrorWrite, 
	cbWrite=> );

fbFileClose(
	sNetId:= , 
	hFile:= fbFileOpen.hFile, 
	bExecute:= , 
	tTimeout:= T#10S, 
	bBusy=> , 
	bError=> , 
	nErrId=> );

bErrorWrite := fbFileOpen.bError OR fbFileWrite.bError;
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="MessageData">
      <LineId Id="3663" Count="0" />
      <LineId Id="3460" Count="0" />
      <LineId Id="3664" Count="0" />
      <LineId Id="3461" Count="2" />
      <LineId Id="3676" Count="0" />
      <LineId Id="3464" Count="1" />
      <LineId Id="3660" Count="0" />
      <LineId Id="3466" Count="0" />
      <LineId Id="3661" Count="0" />
      <LineId Id="3665" Count="0" />
      <LineId Id="3467" Count="3" />
      <LineId Id="3472" Count="0" />
      <LineId Id="3662" Count="0" />
      <LineId Id="3473" Count="6" />
      <LineId Id="3666" Count="0" />
      <LineId Id="3481" Count="4" />
      <LineId Id="3488" Count="2" />
      <LineId Id="3780" Count="0" />
      <LineId Id="3779" Count="0" />
      <LineId Id="3781" Count="3" />
      <LineId Id="3491" Count="1" />
      <LineId Id="3667" Count="0" />
      <LineId Id="3493" Count="2" />
      <LineId Id="3688" Count="0" />
      <LineId Id="3496" Count="1" />
      <LineId Id="3689" Count="0" />
      <LineId Id="3687" Count="0" />
      <LineId Id="3498" Count="0" />
      <LineId Id="3691" Count="0" />
      <LineId Id="3690" Count="0" />
      <LineId Id="3499" Count="10" />
      <LineId Id="3658" Count="0" />
      <LineId Id="3510" Count="1" />
      <LineId Id="3659" Count="0" />
      <LineId Id="3512" Count="2" />
      <LineId Id="3668" Count="0" />
      <LineId Id="3515" Count="21" />
      <LineId Id="3541" Count="2" />
      <LineId Id="3646" Count="0" />
      <LineId Id="3642" Count="3" />
      <LineId Id="3647" Count="0" />
      <LineId Id="3641" Count="0" />
      <LineId Id="3544" Count="13" />
      <LineId Id="3563" Count="2" />
      <LineId Id="3649" Count="0" />
      <LineId Id="3669" Count="0" />
      <LineId Id="3650" Count="1" />
      <LineId Id="3657" Count="0" />
      <LineId Id="3652" Count="4" />
      <LineId Id="3566" Count="0" />
      <LineId Id="3670" Count="0" />
      <LineId Id="3567" Count="1" />
      <LineId Id="3686" Count="0" />
      <LineId Id="3569" Count="3" />
      <LineId Id="3671" Count="0" />
      <LineId Id="3573" Count="4" />
      <LineId Id="3581" Count="12" />
      <LineId Id="3595" Count="2" />
      <LineId Id="3672" Count="0" />
      <LineId Id="3598" Count="32" />
      <LineId Id="3673" Count="1" />
      <LineId Id="3631" Count="0" />
      <LineId Id="3785" Count="0" />
      <LineId Id="3632" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="3675" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.Builder">
      <LineId Id="16" Count="27" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.Device_to_String">
      <LineId Id="196" Count="113" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.FolderDate">
      <LineId Id="10" Count="12" />
      <LineId Id="25" Count="1" />
      <LineId Id="28" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.StringLen">
      <LineId Id="12" Count="9" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MessageData.WriteData">
      <LineId Id="11" Count="2" />
      <LineId Id="124" Count="0" />
      <LineId Id="14" Count="8" />
      <LineId Id="123" Count="0" />
      <LineId Id="25" Count="1" />
      <LineId Id="28" Count="42" />
      <LineId Id="72" Count="3" />
      <LineId Id="122" Count="0" />
      <LineId Id="78" Count="36" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>