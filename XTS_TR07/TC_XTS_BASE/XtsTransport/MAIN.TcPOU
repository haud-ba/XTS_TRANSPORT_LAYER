<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MAIN" Id="{f2e9720a-bda2-4399-8c7d-d9b6e11179c8}" SpecialFunc="None">
    <Declaration><![CDATA[//---------------------------------------------------------------------------------------------------------------
//
//  HAUD 2024.03.18
//
//  - XTS_TRAINING
//  - example project for setting up transport layer
//  - transport layer members:
//    - CaGroup
//      - access to Group functions
//
//    - Mover
//      - access to MC and CA motion functions
//
//    - Xpu (ProcessingUnit)
//      - access to TcCOM Objects in Environment
//
//    - Transport Control Unit
//      - coordinates members to get XTS in defined states
//
//    - Xts Stations
//      - handshakes with extern control for mover transport
//
//  - global datafields for mrmbers in GVL_XTS namespace
//  - global interface datafields for use in members
//  - structures are assigned by reference for every member
//
//---------------------------------------------------------------------------------------------------------------
// This SOFTWARE is provided as an Exemple by THE PROVIDER "as is" and "with all faults." THE PROVIDER makes no 
// representations or warranties of any kind concerning the safety, suitability, lack of viruses, inaccuracies, 
// typographical errors, or other harmful components of this SOFTWARE. There are inherent dangers in the use of 
// any software, and you are solely responsible for determining whether this SOFTWARE is compatible with your 
// equipment and other software installed on your equipment. You are also solely responsible for the protection 
// of your equipment and backup of your data, and THE PROVIDER will not be liable for any damages you may suffer 
// in connection with using, modifying, or distributing this SOFTWARE.
//---------------------------------------------------------------------------------------------------------------
PROGRAM MAIN
VAR
	bInit 				: BOOL;
  eInit         : E_PROGRESS;

  nStation,
	nMover,
  nMoverSelected,
  nStationSelected    : UINT;

  eTransportState     : E_XTS_TRANSPORT_STATE;
  eTransportResult    : E_PROGRESS;

  eXpuState           : E_XPU_STATE;
  eXpuResult          : E_PROGRESS;
  eXpuCheck           : E_XPU_CHECK;

  eMoverState         : E_MOVER_STATE;
  eMoverResult        : E_PROGRESS;

  sCaGroupState,
  sTransportState,
  sTransportCheck,
  sTransportResult,
  sXpuState,
  sXpuResult,
  sXpuCheck,
  sXpuOpMode,
  sXpuMoverdetectionMode,
  sXpuMoverAssignement,
  sMoverState,
  sMoverResult,
  sStationState,
  sStationStateMask,
  sStationCtrlMask        : STRING;

  bStationEnable,
  bStationEnter,
  bStationStart,
  bStationDone,
  bStationOut,
  bStationGone            : BOOL;

  tonWriteLog             : Tc2_Standard.TON;
  tWriteLog               : TIME := T#5S;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//############################################################################
// Local time; used for timestamps
//############################################################################
GVL_MSG.fbLocalTime(
                    sLocalTime                => GVL_MSG.sLocalTime, 
                    dtLocalTime               => GVL_MSG.dtLocalTime);
//############################################################################

//############################################################################
// 	example for init sequence
//############################################################################	
IF NOT bInit
THEN
  CASE eInit
  OF
    E_PROGRESS.PROGRESS_INVALID,
    E_PROGRESS.PROGRESS_DONE:         // clear command interface
      eInit                           := E_PROGRESS.PROGRESS_BUSY;
      GVL_XTS.XpuCtrl.Cmd             := E_XPU_CTRL.XTS_CMD_NULL;

    E_PROGRESS.PROGRESS_BUSY:         // init Xpu, ProcessingUnit, MotorModules
      eInit                           := E_PROGRESS.PROGRESS_PREPARE;
      GVL_XTS.XpuCtrl.Cmd             := E_XPU_CTRL.XTS_CMD_INIT;

    E_PROGRESS.PROGRESS_PREPARE:      // wait for init to be done
      IF (GVL_XTS.XpuState.State      = E_XPU_STATE.XTS_INIT + E_PROGRESS.PROGRESS_DONE)
      THEN
        eInit                         := E_PROGRESS.PROGRESS_STARTUP;
        GVL_XTS.XpuCtrl.Cmd           := E_XPU_CTRL.XTS_IDLE;
      END_IF

    E_PROGRESS.PROGRESS_STARTUP:      // assign mover interfaces
      eInit                           := E_PROGRESS.PROGRESS_CHECK;
      FOR nMover := 1 TO MAX_MOVER
      DO
        GVL_XTS.MoverItf[nMover]      := GVL_XTS.Mover[nMover];
      END_FOR

    E_PROGRESS.PROGRESS_CHECK:        // assign CaGroup interface
      eInit                           := E_PROGRESS.PROGRESS_OCCUPIED;
      GVL_XTS.CaGroupItf              := GVL_XTS.CaGroup;

    E_PROGRESS.PROGRESS_OCCUPIED:     // StartStation for XtsTransportCtrl.Cmd := E_XTS_TRANSPORT_CTRL.CMD_TRANSPORT_START;
      eInit                           := E_PROGRESS.PROGRESS_WORKING;
      // Start Position parameters
      GVL_XTS.StationStart.rPosWait   :=    375.0;
      GVL_XTS.StationStart.rGap       :=    125.0;
      GVL_XTS.StationStart.rVelo      :=   1250.0;
      GVL_XTS.StationStart.rAccDec    :=  12000.0;
      GVL_XTS.StationStart.rJerk      := 400000.0;

    E_PROGRESS.PROGRESS_WORKING:
      GVL_XTS.StationParameter[1].rPosWait            :=   375.0; // absolute modulo position
      GVL_XTS.StationParameter[1].rPosStop[1]         :=   150.0; // relative to rPosWait
      GVL_XTS.StationParameter[1].rReleaseDistance    :=   125.0; // relative to Mover.ActPos

      GVL_XTS.StationParameter[1].rGap                :=     95.0;
      GVL_XTS.StationParameter[1].rVelo               :=   1250.0;
      GVL_XTS.StationParameter[1].rAccDec             :=  12000.0;
      GVL_XTS.StationParameter[1].rJerk               := 400000.0;


      GVL_XTS.StationParameter[2].rPosWait            :=  1375.0; // absolute modulo position
      GVL_XTS.StationParameter[2].rPosStop[1]         :=   150.0; // relative to rPosWait
      GVL_XTS.StationParameter[2].rReleaseDistance    :=   125.0; // relative to Mover.ActPos

      GVL_XTS.StationParameter[2].rGap                :=    105.0;
      GVL_XTS.StationParameter[2].rVelo               :=   1250.0;
      GVL_XTS.StationParameter[2].rAccDec             :=  12000.0;
      GVL_XTS.StationParameter[2].rJerk               := 400000.0;

      bInit                                           := TRUE;
      eInit                                           := E_PROGRESS.PROGRESS_DONE;
  END_CASE
END_IF

//############################################################################
// 	XTS Transport Unit
//############################################################################
//  - main control function block for transport layer
//  - requires access to member controls
//    - Xpu
//    - CaGroup
//    - Mover
//    - Station Control
//    - Station LinkedList Interface
//############################################################################
//
// cyclic call
GVL_XTS.XtsTransport();

// main control
GVL_XTS.XtsTransport.Ctrl               REF= GVL_XTS.XtsTransportCtrl;   // main control
GVL_XTS.XtsTransport.State              REF= GVL_XTS.XtsTransportState;  // main state
                                        
// member controls                      
GVL_XTS.XtsTransport.XpuCtrl            REF= GVL_XTS.XpuCtrl;
GVL_XTS.XtsTransport.XpuState           REF= GVL_XTS.XpuState;
GVL_XTS.XtsTransport.XpuInfo            REF= GVL_XTS.XpuInfo;
                                        
GVL_XTS.XtsTransport.GroupItf           :=   GVL_XTS.CaGroupItf;
GVL_XTS.XtsTransport.GroupInfo          REF= GVL_XTS.CaGroupInfo;
                                        
GVL_XTS.XtsTransport.MoverItf           REF= GVL_XTS.MoverItf;
GVL_XTS.XtsTransport.MoverInfo          REF= GVL_XTS.MoverInfo;
                                        
GVL_XTS.XtsTransport.StationStart       REF= GVL_XTS.StationStart;      // start position for E_XTS_TRANSPORT_CTRL.CMD_TRANSPORT_START
GVL_XTS.XtsTransport.StationCtrlItf     REF= GVL_XTS.StationCtrlItf;    // station interfaces are required for accessing station methods
GVL_XTS.XtsTransport.StationListsItf    REF= GVL_XTS.StationListItf;    // list interface is required for access to linked list methods

// where to start?
GVL_XTS.XtsTransport.StartStationIndex  := 1;
//############################################################################



//############################################################################
// 	XTS Stations
//############################################################################	
FOR nStation := 1 TO MAX_STATION
DO
  GVL_XTS.Station[nStation].StationId   := nStation;

  GVL_XTS.StationListItf[nStation]      := GVL_XTS.StationList[nStation];   // interfaces for list methods
  GVL_XTS.StationCtrlItf[nStation]      := GVL_XTS.Station[nStation];       // interfaces for station methods

  GVL_XTS.Station[nStation].Ctrl        REF= GVL_XTS.StationCtrl;           // station ctrl from extern
  GVL_XTS.Station[nStation].State       REF= GVL_XTS.StationState;          // station state to extern

  GVL_XTS.Station[nStation].ItfStations REF= GVL_XTS.StationListItf;        // give every station all list interfaces for writing mover tickets
  GVL_XTS.Station[nStation].ItfMover    REF= GVL_XTS.MoverItf;              // give every station all mover interfaces for movement in / out of station
  GVL_XTS.Station[nStation].Mover       REF= GVL_XTS.AxisRefMover;          // AXIS_REF to read infos from
  GVL_XTS.Station[nStation].MoverOffset REF= GVL_XTS.PositionOffset;        // static offsets for all stations, all movers, all stop positions


  GVL_XTS.Station[nStation].StationParameter  REF= GVL_XTS.StationParameter;// give every station all station parameters for sending to WaitPos of target station

  // cyclic call
  GVL_XTS.Station[nStation].Cycle();                                        // cycle for handshaking with extern
END_FOR
//############################################################################



//############################################################################
// 	XPU environment
//############################################################################	
GVL_XTS.Xpu.XpuInstance               := 1;
GVL_XTS.Xpu.Ctrl                      REF= GVL_XTS.XpuCtrl;
GVL_XTS.Xpu.State                     REF= GVL_XTS.XpuState;
GVL_XTS.Xpu.XpuModuleInfoData         REF= GVL_XTS.XpuModules;

GVL_XTS.Xpu.CaGroupOID                :=   GVL_XTS.CaGroup.GroupInfo.CaGroupOID;

// cyclic call
GVL_XTS.Xpu();
GVL_XTS.XpuInfo                       :=   GVL_XTS.Xpu.XpuInfo;
//############################################################################


//############################################################################	
// 	CA-Group Class
//############################################################################	
// assignment
GVL_XTS.CaGroup.AxisRef               REF= GVL_XTS.AxisRefMover;
GVL_XTS.CaGroup.GroupRef              REF= GVL_XTS.CaGroupRef;

// cyclic call
GVL_XTS.CaGroup.InfoCycle(TRUE); // read group status

// InfoData from group
GVL_XTS.CaGroupInfo                   :=   GVL_XTS.CaGroup.GroupInfo;

// Interface of CaGroup
GVL_XTS.CaGroupItf                    :=   GVL_XTS.CaGroup;
//############################################################################


//############################################################################
// Mover Class	
//############################################################################	
FOR nMover := 1 TO MAX_MOVER
DO
  GVL_XTS.Mover[nMover].Ctrl          REF= GVL_XTS.MoverCtrl [nMover];    // mover control from extern
  GVL_XTS.Mover[nMover].State         REF= GVL_XTS.MoverState[nMover];    // mover state to extern

  GVL_XTS.Mover[nMover].MoveData      REF= GVL_XTS.MoveData[nMover];      // dyn constraints for extern to write
  GVL_XTS.Mover[nMover].GearData      REF= GVL_XTS.GearData[nMover];      // GearIn/GearInPosCa data fro extern to write

  // assignment	
  GVL_XTS.Mover[nMover].Mover         REF= GVL_XTS.AxisRefMover[nMover];  // axis reference to Mover function block
  GVL_XTS.Mover[nMover].LastGap       REF= GVL_XTS.LastGap[nMover];       // reference to last used gap value datafield
  GVL_XTS.Mover[nMover].LastPosition  REF= GVL_XTS.LastPosition[nMover];  // reference to last position datafield
  
  GVL_XTS.Mover[nMover].RailLength    := GVL_XTS.Xpu.RailLength;          // mover needs RailLength, in order to calc absolute position from modulo input
  
  GVL_XTS.Mover[nMover].MoverInfo		  REF= GVL_XTS.MoverInfo[nMover];     // refernence to mover info data (ActPos, SetPos, ...)

  GVL_XTS.MoverItf[nMover]            := GVL_XTS.Mover[nMover];

  // cyclic call
  GVL_XTS.Mover[nMover].Cycle();  // call AXIS_REF()
  GVL_XTS.Mover[nMover]();        // call Ctrl/State
END_FOR


//############################################################################


//############################################################################
// /logging
//############################################################################
tonWriteLog(IN:= TRUE, PT := tWriteLog);

IF (tonWriteLog.Q)
THEN
  MessageData.bWriteFile      := TRUE;
  tonWriteLog(IN:= FALSE);
END_IF

//-----------------------------------------------------------------------------------------------------------------
// write message list to file
//-----------------------------------------------------------------------------------------------------------------
MessageData(pList			        := ADR(GVL_MSG.Messages),
			      pLocalTime        := ADR(GVL_MSG.dtLocalTime), 
			      bBusy=> );

// clear write trigger
IF MessageData.bWriteFile AND
   MessageData.bBusy
THEN
  MessageData.bWriteFile      := FALSE;
END_IF
//############################################################################
// logging/
//############################################################################



//############################################################################
// Visualization data	
//############################################################################	
// get enumerations from XPU for visu:
eTransportState                       := f_GetState   (GVL_XTS.XtsTransportState.State);
eTransportResult                      := f_GetProgress(GVL_XTS.XtsTransportState.State);

eXpuState                             := f_GetState   (GVL_XTS.XpuState.State);
eXpuResult                            := f_GetProgress(GVL_XTS.XpuState.State);

eXpuCheck                             := GVL_XTS.XpuState.Check;

// display xpu state and result in visu
// get strings and info
sCaGroupState                         := TO_STRING(GVL_XTS.CaGroupInfo.CaGroupState);

sTransportState                       := TO_STRING(eTransportState);
sTransportCheck                       := TO_STRING(GVL_XTS.XtsTransportState.Check);
sTransportResult                      := TO_STRING(eTransportResult);

sXpuState                             := TO_STRING(eXpuState);
sXpuResult                            := TO_STRING(eXpuResult);
sXpuCheck                             := TO_STRING(eXpuCheck);

sXpuOpMode                            := GVL_XTS.Xpu.OpModeToString();
sXpuMoverdetectionMode                := GVL_XTS.Xpu.IdDetectionModeToString();
sXpuMoverAssignement                  := GVL_XTS.Xpu.MoverPositionAssignementToString();

nMoverSelected                        := SEL((nMoverSelected<1), nMoverSelected, 1);
nMoverSelected                        := SEL((nMoverSelected>MAX_MOVER), nMoverSelected, MAX_MOVER);

nStationSelected                      := SEL((nStationSelected<1), nStationSelected, 1);
nStationSelected                      := SEL((nStationSelected>MAX_STATION), nStationSelected, MAX_STATION);

bStationEnable                        := SEL((GVL_XTS.StationCtrl[nStationSelected].eCmd = E_STATION_CTRL.STATION_ENABLE),        FALSE, TRUE);
bStationEnter                         := SEL((GVL_XTS.StationCtrl[nStationSelected].eCmd = E_STATION_CTRL.STATION_MOVER_ENTER),   FALSE, TRUE);
bStationStart                         := SEL((GVL_XTS.StationCtrl[nStationSelected].eCmd = E_STATION_CTRL.STATION_PROCESS_START), FALSE, TRUE);
bStationDone                          := SEL((GVL_XTS.StationCtrl[nStationSelected].eCmd = E_STATION_CTRL.STATION_PROCESS_DONE),  FALSE, TRUE);
bStationOut                           := SEL((GVL_XTS.StationCtrl[nStationSelected].eCmd = E_STATION_CTRL.STATION_MOVER_OUT),     FALSE, TRUE);
bStationGone                          := SEL((GVL_XTS.StationCtrl[nStationSelected].eCmd = E_STATION_CTRL.STATION_MOVER_GONE),    FALSE, TRUE);

eMoverState                           := f_GetState   (GVL_XTS.MoverState[nMoverSelected].State);
eMoverResult                          := f_GetProgress(GVL_XTS.MoverState[nMoverSelected].State);

sMoverState                           := TO_STRING(eMoverState);
sMoverResult                          := TO_STRING(eMoverResult);

sStationState                         := TO_STRING(GVL_XTS.StationState[nStationSelected].eState);
sStationStateMask                     := Tc2_Utilities.BYTE_TO_BINSTR(GVL_XTS.StationState[nStationSelected].nMask, 8);
sStationCtrlMask                      := Tc2_Utilities.BYTE_TO_BINSTR(GVL_XTS.StationCtrl[nStationSelected].nMask, 8);

]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="1414" Count="5" />
      <LineId Id="1413" Count="0" />
      <LineId Id="1420" Count="0" />
      <LineId Id="591" Count="2" />
      <LineId Id="432" Count="0" />
      <LineId Id="435" Count="0" />
      <LineId Id="440" Count="0" />
      <LineId Id="442" Count="0" />
      <LineId Id="444" Count="1" />
      <LineId Id="450" Count="0" />
      <LineId Id="447" Count="0" />
      <LineId Id="449" Count="0" />
      <LineId Id="448" Count="0" />
      <LineId Id="454" Count="0" />
      <LineId Id="451" Count="0" />
      <LineId Id="453" Count="0" />
      <LineId Id="452" Count="0" />
      <LineId Id="455" Count="1" />
      <LineId Id="458" Count="0" />
      <LineId Id="457" Count="0" />
      <LineId Id="459" Count="0" />
      <LineId Id="461" Count="0" />
      <LineId Id="460" Count="0" />
      <LineId Id="607" Count="0" />
      <LineId Id="600" Count="0" />
      <LineId Id="602" Count="0" />
      <LineId Id="601" Count="0" />
      <LineId Id="603" Count="0" />
      <LineId Id="608" Count="0" />
      <LineId Id="605" Count="0" />
      <LineId Id="609" Count="0" />
      <LineId Id="606" Count="0" />
      <LineId Id="610" Count="0" />
      <LineId Id="604" Count="0" />
      <LineId Id="755" Count="0" />
      <LineId Id="1082" Count="0" />
      <LineId Id="746" Count="0" />
      <LineId Id="749" Count="1" />
      <LineId Id="752" Count="2" />
      <LineId Id="747" Count="0" />
      <LineId Id="918" Count="1" />
      <LineId Id="1096" Count="0" />
      <LineId Id="1239" Count="0" />
      <LineId Id="1101" Count="0" />
      <LineId Id="921" Count="4" />
      <LineId Id="1097" Count="2" />
      <LineId Id="1240" Count="0" />
      <LineId Id="1100" Count="0" />
      <LineId Id="928" Count="2" />
      <LineId Id="920" Count="0" />
      <LineId Id="462" Count="1" />
      <LineId Id="443" Count="0" />
      <LineId Id="439" Count="0" />
      <LineId Id="590" Count="0" />
      <LineId Id="554" Count="0" />
      <LineId Id="556" Count="0" />
      <LineId Id="633" Count="0" />
      <LineId Id="613" Count="0" />
      <LineId Id="629" Count="3" />
      <LineId Id="1093" Count="1" />
      <LineId Id="557" Count="0" />
      <LineId Id="619" Count="0" />
      <LineId Id="618" Count="0" />
      <LineId Id="555" Count="0" />
      <LineId Id="627" Count="0" />
      <LineId Id="585" Count="0" />
      <LineId Id="584" Count="0" />
      <LineId Id="586" Count="0" />
      <LineId Id="628" Count="0" />
      <LineId Id="615" Count="2" />
      <LineId Id="655" Count="0" />
      <LineId Id="588" Count="0" />
      <LineId Id="587" Count="0" />
      <LineId Id="589" Count="0" />
      <LineId Id="595" Count="0" />
      <LineId Id="594" Count="0" />
      <LineId Id="598" Count="0" />
      <LineId Id="718" Count="1" />
      <LineId Id="1087" Count="0" />
      <LineId Id="915" Count="0" />
      <LineId Id="1171" Count="0" />
      <LineId Id="1091" Count="0" />
      <LineId Id="917" Count="0" />
      <LineId Id="641" Count="0" />
      <LineId Id="818" Count="5" />
      <LineId Id="828" Count="1" />
      <LineId Id="836" Count="0" />
      <LineId Id="850" Count="0" />
      <LineId Id="848" Count="0" />
      <LineId Id="1169" Count="0" />
      <LineId Id="837" Count="0" />
      <LineId Id="827" Count="0" />
      <LineId Id="832" Count="0" />
      <LineId Id="844" Count="0" />
      <LineId Id="835" Count="0" />
      <LineId Id="838" Count="2" />
      <LineId Id="847" Count="0" />
      <LineId Id="846" Count="0" />
      <LineId Id="843" Count="0" />
      <LineId Id="853" Count="0" />
      <LineId Id="852" Count="0" />
      <LineId Id="851" Count="0" />
      <LineId Id="830" Count="0" />
      <LineId Id="824" Count="2" />
      <LineId Id="614" Count="0" />
      <LineId Id="421" Count="0" />
      <LineId Id="423" Count="1" />
      <LineId Id="428" Count="0" />
      <LineId Id="422" Count="0" />
      <LineId Id="425" Count="0" />
      <LineId Id="611" Count="1" />
      <LineId Id="426" Count="0" />
      <LineId Id="429" Count="0" />
      <LineId Id="427" Count="0" />
      <LineId Id="430" Count="1" />
      <LineId Id="553" Count="0" />
      <LineId Id="640" Count="0" />
      <LineId Id="635" Count="0" />
      <LineId Id="306" Count="1" />
      <LineId Id="389" Count="0" />
      <LineId Id="308" Count="5" />
      <LineId Id="413" Count="0" />
      <LineId Id="314" Count="0" />
      <LineId Id="414" Count="0" />
      <LineId Id="560" Count="0" />
      <LineId Id="559" Count="0" />
      <LineId Id="315" Count="0" />
      <LineId Id="562" Count="0" />
      <LineId Id="639" Count="0" />
      <LineId Id="561" Count="0" />
      <LineId Id="636" Count="0" />
      <LineId Id="316" Count="0" />
      <LineId Id="390" Count="0" />
      <LineId Id="317" Count="1" />
      <LineId Id="995" Count="1" />
      <LineId Id="998" Count="0" />
      <LineId Id="997" Count="0" />
      <LineId Id="999" Count="0" />
      <LineId Id="994" Count="0" />
      <LineId Id="319" Count="7" />
      <LineId Id="993" Count="0" />
      <LineId Id="563" Count="0" />
      <LineId Id="327" Count="2" />
      <LineId Id="1000" Count="0" />
      <LineId Id="332" Count="0" />
      <LineId Id="620" Count="0" />
      <LineId Id="399" Count="0" />
      <LineId Id="621" Count="0" />
      <LineId Id="1421" Count="28" />
      <LineId Id="638" Count="0" />
      <LineId Id="637" Count="0" />
      <LineId Id="625" Count="0" />
      <LineId Id="622" Count="2" />
      <LineId Id="480" Count="0" />
      <LineId Id="649" Count="0" />
      <LineId Id="690" Count="0" />
      <LineId Id="648" Count="0" />
      <LineId Id="501" Count="0" />
      <LineId Id="499" Count="0" />
      <LineId Id="531" Count="0" />
      <LineId Id="530" Count="0" />
      <LineId Id="505" Count="0" />
      <LineId Id="472" Count="0" />
      <LineId Id="481" Count="0" />
      <LineId Id="681" Count="0" />
      <LineId Id="680" Count="0" />
      <LineId Id="650" Count="1" />
      <LineId Id="683" Count="0" />
      <LineId Id="654" Count="0" />
      <LineId Id="474" Count="0" />
      <LineId Id="478" Count="1" />
      <LineId Id="507" Count="0" />
      <LineId Id="2" Count="0" />
      <LineId Id="489" Count="1" />
      <LineId Id="1085" Count="0" />
      <LineId Id="1084" Count="0" />
      <LineId Id="1086" Count="0" />
      <LineId Id="1310" Count="2" />
      <LineId Id="1328" Count="0" />
      <LineId Id="1327" Count="0" />
      <LineId Id="1330" Count="4" />
      <LineId Id="1005" Count="0" />
      <LineId Id="644" Count="0" />
      <LineId Id="1010" Count="0" />
      <LineId Id="1012" Count="0" />
      <LineId Id="1011" Count="0" />
      <LineId Id="1013" Count="0" />
      <LineId Id="1315" Count="0" />
      <LineId Id="1014" Count="0" />
      <LineId Id="1317" Count="0" />
      <LineId Id="1319" Count="0" />
      <LineId Id="1006" Count="0" />
      <LineId Id="491" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>